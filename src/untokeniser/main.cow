sub next_raw_token(): (token: uint16)
    tokens_fcb.address := &token as [int8];
    tokens_fcb.count := 2;
    fcb_read(&tokens_fcb);
end sub;

sub print_all_tokens()
    loop
        var token: uint16;
        loop
            token := next_raw_token();
            if token == 0 then
                return;
            end if;

            $if DEBUG
                where_am_i();
                print(" token ");
                print_hex_i16(token);
                print_newline();
            $endif

            if token == TOKEN_FILENAME then
                filename_id := next_raw_token();
                print("\n# new file: ");
                show_identifier(filename_id);
                print_newline();
            elseif token == TOKEN_LINENUMBER then
                line_no := next_raw_token();
                print_newline();
            else
                break;
            end if;
        end loop;

        var thing_kind: uint8 := get_thing_type(token);
        if (thing_kind == THING_IDENTIFIER) or (thing_kind == THING_TOKEN) then
            show_identifier(token);
        elseif (thing_kind == THING_STRING) then
            print_char('"');
            show_identifier(token);
            print_char('"');
        elseif (thing_kind == THING_NUMBER) then
            var n: int32 := get_value_of_number(token);
            print("0x");
            print_hex_i32(n as uint32);
        else
            print("?(unknown token 0x");
            print_hex_i16(token);
            print(")");
        end if;

        print_char(' ');
    end loop;
end sub;
print_all_tokens();
print_newline();
