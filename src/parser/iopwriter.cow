var ioptab_top: [int8] := HIMEM;
var ioptab_size: uint16 := 0;
var iopfd: uint8;

sub emit(kind: uint8, size: uint8, out ptr: [Iop])
    ioptab_size := ioptab_size + 1;
    ioptab_top[0-ioptab_size] := size;
    ioptab_size := ioptab_size + size;
    ptr := &ioptab_top[0-ioptab_size] as [Iop];
    zero_memory(ptr as [int8], size);
    ptr.iop_kind := kind;
    ptr.iop_size := size;
end sub;

sub emit_filename(thing_id: uint16)
    if thing_id != 0 then
        var iop: [FilenameIop] := emit(IOP_FILENAME, FilenameIop@bytes) as [FilenameIop];
        iop.thing_id := thing_id;
    end if;
end sub;

sub emit_linenumber(line_number: uint16)
    if line_number != 0 then
        var iop: [LinenumberIop] := emit(IOP_LINENUMBER, LinenumberIop@bytes) as [LinenumberIop];
        iop.line_number := line_number;
    end if;
end sub;

sub emit_simple(kind: uint8)
    var iop: [Iop] := emit(kind, Iop@bytes);
end sub;

sub emit_simple_typed(kind: uint8)
    var iop: [TypedIop] := emit(kind, TypedIop@bytes) as [TypedIop];
end sub;

sub emit_label(label_id: uint16)
    var label_iop: [LabelIop] := emit(IOP_LABEL, LabelIop@bytes) as [LabelIop];
    label_iop.label_id := label_id;
end sub;

sub emit_goto(label_id: uint16)
    var goto_iop: [GotoIop] := emit(IOP_GOTO, GotoIop@bytes) as [GotoIop];
    goto_iop.label_id := label_id;
end sub;

sub emit_conditional_branch(iopcode: uint8, iftrue_label_id: uint16, iffalse_label_id: uint16)
    var iop: [ConditionalIop] := emit(iopcode, ConditionalIop@bytes) as [ConditionalIop];
    iop.iftrue_label_id := iftrue_label_id;
    iop.iffalse_label_id := iffalse_label_id;
end sub;

sub emit_cast(type_id: uint16)
    var iop: [TypedIop] := emit(IOP_CASTOP, TypedIop@bytes) as [TypedIop];
    iop.type_id := type_id;
end sub;

sub emit_parameter(kind: uint8, subroutine_id: uint16, parameter: uint8)
    var iop: [ParameterIop] := emit(kind, ParameterIop@bytes) as [ParameterIop];
    iop.subroutine_id := subroutine_id;
    iop.parameter := parameter;
end sub;

sub create_label(out label_id: uint16)
    label_id := create_thing(THING_LABEL, LabelThing@bytes);
end sub;

sub merge_label(srclabel_id: uint16, destlabel_id: uint16)
    var srclabel: [LabelThing];
    loop
        srclabel := find_thing(srclabel_id) as [LabelThing];
        if srclabel.thing_type == THING_LABEL then
            break;
        else
            srclabel_id := srclabel.target;
        end if;
    end loop;

    srclabel.thing_type := THING_LABELREF;
    srclabel.target := destlabel_id;
end sub;

sub flush_iops(object_id: uint16, oldtop: uint16)
    var header: IHeader[1];
    header[0].thing_id := object_id;
    file_putblock(iopfd, &header[0] as [int8], IHeader@bytes);

    var offset: uint16 := oldtop;
    while offset < ioptab_size loop
        offset := offset + 1;
        var size: uint8 := ioptab_top[0-offset];
        offset := offset + size;
        var iop: [Iop] := &ioptab_top[0-offset] as [Iop];
        file_putblock(iopfd, iop as [int8], size);
    end loop;

    ioptab_size := oldtop;
end sub;

sub open_iopfile(filename: [int8])
    iopfd := file_openout(filename);
end sub;

sub close_iopfile()
    var buf: IHeader[1];
    buf[0].thing_id := 0;
    file_putblock(iopfd, &buf[0] as [int8], IHeader@bytes);
    file_close(iopfd);
end sub;
