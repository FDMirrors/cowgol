var ioptab: uint8[IOPTABLE_SIZE];
var ioptab_top: uint16 := 0;
var iopfd: int8;

sub emit(kind: uint8, size: uint8): (ptr: [Iop])
    ptr := &ioptab[ioptab_top] as [Iop];
    ioptab_top := ioptab_top + (size as uint16);
    if ioptab_top > ioptab@size then
        print("iop table overflow (0x");
        print_hex_i16(ioptab@size);
        print(" bytes)");
        halt();
    end if;

    zero_memory(ptr as [int8], size as uint16);
    ptr.iop_kind := kind;
    ptr.iop_size := size;
end sub;

sub emit_filename(thing_id: uint16)
    if thing_id != 0 then
        var iop: [FilenameIop] := emit(IOP_FILENAME, FilenameIop@bytes) as [FilenameIop];
        iop.thing_id := thing_id;
    end if;
end sub;

sub emit_linenumber(line_number: uint16)
    if line_number != 0 then
        var iop: [LinenumberIop] := emit(IOP_LINENUMBER, LinenumberIop@bytes) as [LinenumberIop];
        iop.line_number := line_number;
    end if;
end sub;

sub emit_untyped_fe(kind: uint8)
    var iop: [Iop] := emit(kind, Iop@bytes);
end sub;

sub emit_fe(kind: uint8)
    var iop: [Iop] := emit(kind, FETypedIop@bytes);
end sub;

sub emit_fe_push(thing_id: uint16)
    var iop: [FEPushIop] := emit(IOP_FEX_PUSH, FEPushIop@bytes) as [FEPushIop];
    iop.thing_id := thing_id;
end sub;

sub emit_fe_push_const(value: int32)
    emit_fe_push(add_number_by_value(value));
end sub;

sub emit_fe_parameter(kind: uint8, subroutine_id: uint16, parameter: uint8)
    var iop: [FEParameterIop] := emit(kind, FEParameterIop@bytes) as [FEParameterIop];
    iop.subroutine_id := subroutine_id;
    iop.parameter := parameter;
end sub;

sub emit_fe_member(thing_id: uint16)
    var iop: [FEMemberIop] := emit(IOP_FEX_MEMBEROF, FEMemberIop@bytes) as [FEMemberIop];
    iop.thing_id := thing_id;
end sub;

sub emit_fe_cast(type_id: uint16)
    var iop: [FETypedIop] := emit(IOP_FE_CAST, FETypedIop@bytes) as [FETypedIop];
    iop.type_id := type_id;
end sub;

sub emit_simple(kind: uint8)
    var iop: [Iop] := emit(kind, Iop@bytes);
end sub;

sub emit_label(label: uint8)
    var iop: [LabelIop] := emit(IOP_LABEL, LabelIop@bytes) as [LabelIop];
    iop.label := label;
end sub;

sub emit_clonelabel(src_label: uint8, dest_label: uint8)
    var iop: [CloneLabelIop] := emit(IOP_CLONELABEL, CloneLabelIop@bytes) as [CloneLabelIop];
    iop.src_label := src_label;
    iop.dest_label := dest_label;
end sub;

sub emit_goto(label: uint8)
    var iop: [GotoIop] := emit(IOP_GOTO, GotoIop@bytes) as [GotoIop];
    iop.label := label;
end sub;

sub emit_named_goto(label_id: uint16)
    var iop: [NamedGotoIop] := emit(IOP_NAMEDGOTO, NamedGotoIop@bytes) as [NamedGotoIop];
    iop.label_id := label_id;
end sub;

sub emit_fe_conditional_branch(iopcode: uint8, iftrue_label: uint8, iffalse_label: uint8)
    var iop: [FEConditionalIop] :=
        emit(iopcode, FEConditionalIop@bytes) as [FEConditionalIop];
    iop.iftrue_label := iftrue_label;
    iop.iffalse_label := iffalse_label;
end sub;

sub emit_byte(value: uint8)
    var iop: [BytesIop] := emit(IOP_BYTES, BytesIop@bytes) as [BytesIop];
    iop.byte_count := 1;
    iop.bytes[0] := value;
end sub;

sub emit_bytes(byte_count: uint8, bytes: [uint8])
    var iop: [BytesIop] := emit(IOP_BYTES, BytesIop@bytes) as [BytesIop];
    iop.byte_count := byte_count;
    copy_memory(bytes as [int8], &iop.bytes[0] as [int8], byte_count as uint16);
end sub;

sub emit_word(word: uint16)
    var buf: uint16[1];
    buf[0] := word;
    emit_bytes(2, &buf[0] as [uint8]);
end sub;

sub emit_address(opcode: uint8, thing_id: uint16, offset: int16)
    var iop: [AddressIop] := emit(opcode, AddressIop@bytes) as [AddressIop];
    iop.thing_id := thing_id;
    iop.offset := offset as uint16;
end sub;

sub create_label(): (label: uint8)
    var subroutine: [SubroutineThing] := find_thing(current_subroutine_id) as [SubroutineThing];
    label := subroutine.label_count;
    if label == 0xff then
        print("too many labels");
        halt();
    end if;
    subroutine.label_count := subroutine.label_count + 1;
end sub;

sub flush_iops(object_id: uint16, oldtop: uint16)
    var header: IHeader;
    header.thing_id := object_id;
    file_putblock(iopfd, &header as [int8], IHeader@bytes);
    file_putblock(iopfd, &ioptab[oldtop] as [int8], ioptab_top - oldtop);
    ioptab_top := oldtop;
end sub;

sub open_iopfile(filename: [int8])
    iopfd := file_openup(filename);

    # So, the last subroutine of the existing iop file is always the main one.
    # We can't append to it, because we need to write other subroutines before
    # *our* main subroutine. So instead we patch it so it ends with a jump to
    # our new one.

    file_seek(iopfd, file_ext(iopfd) - Iop@bytes*3);

    var iop: CallIop;
    zero_memory(&iop as [int8], iop@bytes);
    iop.iop_size := iop@bytes;
    iop.iop_kind := IOP_CALL;
    iop.thing_id := current_subroutine_id;
    file_putblock(iopfd, &iop as [int8], CallIop@bytes);

    iop.iop_size := Iop@bytes;
    iop.iop_kind := IOP_RETURN;
    file_putblock(iopfd, &iop as [int8], Iop@bytes);
    
    iop.iop_size := Iop@bytes;
    iop.iop_kind := IOP_END;
    file_putblock(iopfd, &iop as [int8], Iop@bytes);
end sub;

sub close_iopfile()
    var buf: IHeader[1];
    buf[0].thing_id := 0;
    file_putblock(iopfd, &buf[0] as [int8], IHeader@bytes);
    file_close(iopfd);
end sub;
