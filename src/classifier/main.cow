sub process_subroutine()
    add_reference(current_subroutine_id, 0);
    var subroutine: [SubroutineThing] := find_thing(current_subroutine_id) as [SubroutineThing];
    subroutine.segment := SEGMENT_OMIT;

    sub do_call(iop: [CallIop])
        reference_count := reference_count + 1;
        add_reference(current_subroutine_id, iop.thing_id);
    end sub;

    sub do_ea(ea: [EffectiveAddress])
        arch_classify_ea(ea);
    end sub;

    sub do_beiop(iop: [BEIop])
        arch_classify_iop(iop);
        do_ea(&iop.left);
        do_ea(&iop.right);
        do_ea(&iop.dest);
    end sub;

    sub do_beconditionaliop(iop: [BEConditionalIop])
        arch_classify_conditional_iop(iop);
        do_ea(&iop.left);
        do_ea(&iop.right);
    end sub;

    sub do_addressiop(iop: [AddressIop])
        var obj_id: uint16 := iop.thing_id;
        if get_thing_type(obj_id) == THING_SUBROUTINE then
            add_reference(current_subroutine_id, obj_id);
        end if;
        if is_typed_thing(obj_id) != 0 then
            arch_classify_var(obj_id);
        end if;
    end sub;

    loop
        var iop: [Iop] := read_iop();
        var iopkind: uint8 := iop.iop_kind;
        var ioptype: uint8 := iopkind & IOP_TYPE_MASK;

        if iopkind == IOP_END then
            break;
        elseif iopkind == IOP_CALL then
            do_call(iop as [CallIop]);
        elseif (ioptype == IOP_TYPE_BACKEND_2OP)
            or (ioptype == IOP_TYPE_BACKEND_3OP)
        then
            do_beiop(iop as [BEIop]);
        elseif (iopkind == IOP_BE_BEQ)
            or (iopkind == IOP_BE_BLTS)
            or (iopkind == IOP_BE_BLTU)
            or (iopkind == IOP_BE_BGTS)
            or (iopkind == IOP_BE_BGTU)
        then
            do_beconditionaliop(iop as [BEConditionalIop]);
        elseif (iopkind == IOP_ADDRESS)
            or (iopkind == IOP_ADDRESSLO)
            or (iopkind == IOP_ADDRESSHI)
        then
            do_addressiop(iop as [AddressIop]);
        end if;
    end loop;
end sub;

sub build_graph()
    loop
        var iheader: [IHeader] := read_iop_header();
        if iheader.thing_id == 0 then
            break;
        end if;

        current_subroutine_id := iheader.thing_id;
        process_subroutine();
        subroutine_count := subroutine_count + 1;
    end loop;
end sub;

sub mark_main_subroutines()
    var main_id: uint16 := find_main_subroutine();
    var main: [SubroutineThing] := find_thing(main_id) as [SubroutineThing];
    main.segment := SEGMENT_CODE;
    used_subroutine_count := 1;
end sub;

sub find_reachable_subroutines()
    var changed: uint8;

    loop
        changed := 0;
        var i: uint16 := 0;
        while i < graph_count loop
            var g: [GraphRecord] := &graph_base[i];
            var src: [SubroutineThing] := find_thing(g.src_id) as [SubroutineThing];
            var dest: [SubroutineThing] := find_thing(g.dest_id) as [SubroutineThing];

            if src.segment == SEGMENT_CODE then
                if dest.segment == SEGMENT_OMIT then
                    changed := 1;
                    dest.segment := SEGMENT_CODE;
                    dest.address := used_subroutine_count;
                    used_subroutine_count := used_subroutine_count + 1;
                end if;
            end if;

            i := i + 1;
        end loop;

        if changed == 0 then
            break;
        end if;
    end loop;
end sub;

sub clean_graph()
    var i: uint16 := 0;
    while i < graph_count loop
        var g: [GraphRecord] := &graph_base[i];
        var src: [SubroutineThing] := find_thing(g.src_id) as [SubroutineThing];

        if src.segment == SEGMENT_OMIT then
            g.src_id := 0;
            g.dest_id := 0;
        end if;

        i := i + 1;
    end loop;
end sub;

sub print_graph()
    var i: uint16 := 0;
    while i < graph_count loop
        var g: [GraphRecord] := &graph_base[i];
        show_name_of(g.src_id);
        print(" -> ");
        show_name_of(g.dest_id);
        print_newline();

        i := i + 1;
    end loop;
end sub;

sub assign_space_for_variables(subroutine_id: uint16)
    var subroutine: [SubroutineThing] := find_thing(subroutine_id) as [SubroutineThing];
    var sr: [SubRecord] := &subdata[subroutine.address];

    sub allocate_space()
        var id: uint16 := 1;
        loop
            var thing: [TypedThing] := &thing_table[id] as [TypedThing];
            if thing.thing_size == 0 then
                break;
            end if;
            if (thing.thing_type & THING_TYPED) != 0 then
                if (thing.owner_id == subroutine_id) and (thing.segment != SEGMENT_UNKNOWN) then
                    arch_allocate_var(&sr.base, thing, get_width_of_type(thing.type_id));

                    #show_name_of(subroutine_id);
                    #print_char('.');
                    #show_name_of(id);
                    #print(": segment 0x");
                    #print_hex_i8(thing.segment);
                    #print(" offset 0x");
                    #print_hex_i16(thing.address);
                    #print_newline();
                end if;
            end if;
            id := id + thing.thing_size;
        end loop;
    end sub;

    sub propagate_offsets_to_callees()
        var i: uint16 := 0;
        while i < graph_count loop
            var g: [GraphRecord] := &graph_base[i];
            if g.src_id == subroutine_id then
                if g.dest_id != 0 then
                    var dest: [SubroutineThing] := find_thing(g.dest_id) as [SubroutineThing];
                    var dest_sr: [SubRecord] := &subdata[dest.address];
                    arch_propagate_var_offsets(&sr.base, &dest_sr.base);
                    g.dest_id := 0;
                end if;
                g.src_id := 0;
            end if;
            i := i + 1;
        end loop;
    end sub;

    allocate_space();
    propagate_offsets_to_callees();
    arch_propagate_var_offsets(&sr.base, &root.archsizes);
    subroutine.address := 0; # reset field after internal classifier use
end sub;

sub find_orphaned_subroutine(): (subroutine_id: uint16)
    var i: uint16 := 0;
    while i < graph_count loop
        var g1: [GraphRecord] := &graph_base[i];
        if g1.src_id != 0 then
            var j: uint16 := 0;
            while j < graph_count loop
                var g2: [GraphRecord] := &graph_base[j];
                # Bail out if we find any subroutine called by the one under consideration.
                if g2.dest_id == g1.src_id then
                    break;
                end if;
                j := j + 1;
            end loop;
            if g2.dest_id != g1.src_id then
                subroutine_id := g1.src_id;
                return;
            end if;
        end if;
        i := i + 1;
    end loop;

    subroutine_id := 0;
end sub;

sub assign_space_for_all_variables()
    loop
        var subroutine_id: uint16 := find_orphaned_subroutine();
        if subroutine_id == 0 then
            break;
        end if;

        print("assigning ");
        show_name_of(subroutine_id);
        print_newline();

        assign_space_for_variables(subroutine_id);
    end loop;
end sub;

sub show_subroutine_sizes()
    var id: uint16 := 1;

    sub do_sub(thing: [SubroutineThing])
        if thing.segment == SEGMENT_CODE then
            var sr: [SubRecord] := &subdata[thing.address];
            show_name_of(id);
            print(" ");
            print_i16(thing.address);
            print(": zp=");
            print_i16(sr.base.zp_size);
            print(" data=");
            print_i16(sr.base.data_size);
            print_newline();
        end if;
    end sub;

    loop
        var thing: [Thing] := &thing_table[id] as [Thing];
        if thing.thing_size == 0 then
            break;
        end if;
        if thing.thing_type == THING_SUBROUTINE then
            do_sub(thing as [SubroutineThing]);
        end if;
        id := id + thing.thing_size;
    end loop;
end sub;

init_graph(LOMEM);
build_graph();
print("Total subroutines: ");
print_i16(subroutine_count);
print("\n(");
print_i16(graph_count);
print(" graph nodes from ");
print_i16(reference_count);
print(" references)\n");

mark_main_subroutines();
find_reachable_subroutines();

print("Reachable subroutines: ");
print_i16(used_subroutine_count);
print_newline();

clean_graph();
#print_graph();
init_subdata(&graph_base[graph_count] as [int8]);
assign_space_for_all_variables();
#show_subroutine_sizes();

arch_report_offsets(&root.archsizes);

save_thing_table("things.dat");
