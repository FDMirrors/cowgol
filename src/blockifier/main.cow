var labels: uint8[256];

sub pass1()
    sub do_label(iop: [LabelIop])
        labels[iop.label] := iop.label;
    end sub;

    sub do_clonelabel(iop: [CloneLabelIop])
        labels[iop.src_label] := iop.dest_label;
    end sub;

    loop
        var iop: [Iop] := read_iop();
        var iopkind: uint8 := iop.iop_kind;

        if iopkind == IOP_END then
            break;
        elseif iopkind == IOP_LABEL then
            do_label(iop as [LabelIop]);
        elseif iopkind == IOP_CLONELABEL then
            do_clonelabel(iop as [CloneLabelIop]);
        end if;
    end loop;
end sub;

sub pass2()
    sub lookup_label(labelin: uint8): (labelout: uint8)
        loop
            labelout := labels[labelin];
            if labelout == labelin then
                return;
            end if;
            labelin := labelout;
        end loop;
    end sub;

    sub do_label(iop: [LabelIop])
        iop.label := lookup_label(iop.label);
        write_iop(iop as [Iop]);
    end sub;

    sub do_goto(iop: [GotoIop])
        iop.label := lookup_label(iop.label);
        write_iop(iop as [Iop]);
    end sub;

    sub do_conditional(iop: [BEConditionalIop])
        iop.iftrue_label := lookup_label(iop.iftrue_label);
        iop.iffalse_label := lookup_label(iop.iffalse_label);
        write_iop(iop as [Iop]);
    end sub;

    loop
        var iop: [Iop] := read_iop();
        var iopkind: uint8 := iop.iop_kind;

        if iopkind == IOP_END then
            break;
        elseif iopkind == IOP_LABEL then
            do_label(iop as [LabelIop]);
        elseif iopkind == IOP_CLONELABEL then
            # do nothing
        elseif iopkind == IOP_GOTO then
            do_goto(iop as [GotoIop]);
        elseif (iopkind & IOP_TYPE_MASK) == IOP_TYPE_BACKEND_SPECIAL then
            do_conditional(iop as [BEConditionalIop]);
        else
            write_iop(iop);
        end if;
    end loop;

    write_untyped_iop(IOP_END);
end sub;

sub skip_subroutine()
    loop
        var iop: [Iop] := read_iop();
        if iop.iop_kind == IOP_END then
            break;
        end if;
    end loop;
end sub;

sub process_subroutines()
    loop
        var iheader: [IHeader] := read_iop_header();
        if iheader.thing_id == 0 then
            break;
        end if;

        current_subroutine_id := iheader.thing_id;
        current_subroutine := find_thing(current_subroutine_id) as [SubroutineThing];
        current_subroutine_id := iheader.thing_id;
        if current_subroutine.segment == SEGMENT_OMIT then
            skip_subroutine();
        else
            zero_memory(&labels[0] as [int8], labels@bytes);
            write_iop_header(iheader);

            var pos: uint32 := file_tell(iop_in_fd);
            pass1();

            file_seek(iop_in_fd, pos);
            pass2();
        end if;
    end loop;
    write_terminating_iop_header();
end sub;

process_subroutines();
