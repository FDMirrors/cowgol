sub open_with_osfind(filename: [int8], a: uint8): (fd: uint8)
    var t: [int8] := filename;
    loop
        if t[0] == 0 then
            break;
        end if;
        t := t + 1;
    end loop;
    t[0] := 13;

    @bytes 0xAD, &a;            # LDA a
    @bytes 0xAE, &filename+0;   # LDX ptr+0
    @bytes 0xAC, &filename+1;   # LDY ptr+1
    @bytes 0x20, 0xCE, 0xFF;    # JSR OSFIND
    @bytes 0x8D, &fd;           # STA abs

    t[0] := 0;
end sub;

sub file_openin(filename: [int8]): (fd: uint8)
    fd := open_with_osfind(filename, 0x40);
end sub;

sub file_openout(filename: [int8]): (fd: uint8)
    fd := open_with_osfind(filename, 0x80);
end sub;

sub file_openup(filename: [int8]): (fd: uint8)
    fd := open_with_osfind(filename, 0xC0);
end sub;

sub file_getblock(fd: uint8, ptr: [int8], length: uint16)
end sub;

sub file_putblock(fd: uint8, ptr: [int8], length: uint16)
end sub;

sub file_seek(fd: uint8, pos: uint32)
end sub;

sub file_ext(fd: uint8): (length: uint32)
end sub;

sub file_eof(fd: uint8): (eof: uint8)
    @bytes 0xA9, 0x7F;          # LDA #7F
    @bytes 0xAE, &fd;           # LDX fd
    @bytes 0x20, 0xF4, 0xFF;    # JSR OSBYTE
    @bytes 0x8E, &eof;          # STX eof
end sub;

sub file_putchar(fd: uint8, char: int8)
    @bytes 0xAD, &char;         # LDA char
    @bytes 0xAC, &fd;           # LDY fd
    @bytes 0x20, 0xD4, 0xFF;    # JSR OSBPUT
end sub;

sub file_getchar(fd: uint8): (char: int8)
    @bytes 0xAC, &fd;           # LDY fd
    @bytes 0x20, 0xD7, 0xFF;    # JSR OSBGET
    @bytes 0x8D, &char;         # STA char
end sub;

sub file_close(fd: uint8)
    @bytes 0xA9, 0x00;          # LDA #0
    @bytes 0xAC, &fd;           # LDY fd
    @bytes 0x20, 0xCE, 0xFF;    # JSR OSFIND
end sub;
