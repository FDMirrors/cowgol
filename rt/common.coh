sub print(ptr: [uint8])
    loop
        var c := [ptr];
        if c == 0 then
            return;
        end if;
        print_char(c);
        ptr := ptr + 1;
    end loop;
end sub;

sub print_nl()
	print_char('\n');
end sub;

sub UIToA(value: uint32, base: uint8, buffer: [uint8]): (ptr: [uint8])
	ptr := buffer;
	loop
		var rem: uint32;
		(value, rem) := divmodu32(value, base as uint32);
		if rem < 10 then
			rem := rem + '0';
		else
			rem := rem + ('a' - 10);
		end if;
		[ptr] := rem as uint8;
		ptr := @next ptr;

		if value == 0 then
			break;
		end if;
	end loop;

	var s1 := buffer;
	var s2 := @prev ptr;
	while s2 > s1 loop
		var c := [s1];
		[s1] := [s2];
		[s2] := c;
		s1 := @next s1;
		s2 := @prev s2;
	end loop;

	[ptr] := 0;
end sub;

sub IToA(value: int32, base: uint8, buffer: [uint8]): (ptr: [uint8])
	if value < 0 then
		[buffer] := '-';
		buffer := @next buffer;
		value := -value;
	end if;
	ptr := UIToA(value as uint32, base, buffer);
end sub;

sub print_i32(value: uint32)
    var buffer: uint8[12];
	var pe := UIToA(value, 10, &buffer[0]);
	print(&buffer[0]);
end sub;

sub print_i16(value: uint16)
    print_i32(value as uint32);
end sub;

sub print_i8(value: uint8)
    print_i32(value as uint32);
end sub;

sub print_hex_i8(value: uint8)
    var i: uint8 := 2;
    loop
        var digit := value >> 4;
        if digit < 10 then
            digit := digit + '0';
        else
            digit := digit + ('a' - 10);
        end if;
        print_char(digit);
        value := value << 4;
        i := i - 1;
        if i == 0 then
            break;
        end if;
    end loop;
end sub;

sub print_hex_i16(value: uint16)
    print_hex_i8((value >> 8) as uint8);
    print_hex_i8(value as uint8);
end sub;

sub print_hex_i32(value: uint32)
    print_hex_i8((value >> 24) as uint8);
    print_hex_i8((value >> 16) as uint8);
    print_hex_i8((value >> 8) as uint8);
    print_hex_i8(value as uint8);
end sub;

sub ExitWithError()
	Exit();
end sub;

sub MemZero(ptr: [uint8], size: intptr)
	MemSet(ptr, 0, size);
end sub;

