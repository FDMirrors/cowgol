const STACK_SIZE := 128;

var workspaceSize: Size[NUM_WORKSPACES];

sub E_nl()
	EmitByte('\n');
end sub;

sub ArchAlignUp(value: Size, alignment: uint8): (newvalue: Size)
	newvalue := value;
end sub;

sub ArchEmitSubRef(subid: uint16)
	E("f");
	E_u16(subid);
end sub;

sub ArchEmitWSRef(wid: uint8, address: Size)
	E("ws+");
	E_u16(address);
end sub;

sub ArchEmitHeader(coo: [Coo])
	E("\t.arch i386\n");
	E("\t.code32\n");
	E(".global _start\n");
	E("\t_start:\n");
	E("\tlea 4(%esp), %eax\n");
	E("\tmov %eax, (argv)\n");

	while coo != (0 as [Coo]) loop
		var main := coo.index.subroutines[0];
		if main != (0 as [Subroutine]) then
			E("\tcall ");
			ArchEmitSubRef(main.id);
			E_nl();
		end if;
		coo := coo.next;
	end loop;

	E("_exit:\n");
	E("\tmov $1, %eax\n");
	E("\txor %ebx, %ebx\n");
	E("\tint $0x80\n");
end sub;

sub ArchEmitFooter(coo: [Coo])
	E("\t.bss\n");
	E("argv: .space 4\n");
	E("ws: .space ");
	E_u16(workspaceSize[0]);
	E_nl();
	E("LOMEM: .space 1024*1024\n");
	E("HIMEM:\n");

	# This is ridiculous, but can't be helped: write spaces to pad the end
	# of the file. Unix C compilers don't like trailing nuls.

	var padding := FCBExt(&outfcb) - FCBPos(&outfcb);
	while padding != 0 loop
		EmitByte(' ');
		padding := padding - 1;
	end loop;
end sub;


