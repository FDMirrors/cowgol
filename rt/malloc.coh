record MallocFreeBlock
	next: [MallocFreeBlock];
	size: intptr; # bytes
end record;

record MallocUsedBlock
	size: intptr; # bytes
end record;

var freeList: [MallocFreeBlock] := LOMEM as [MallocFreeBlock];
freeList.next := 0 as [MallocFreeBlock];
freeList.size := HIMEM - LOMEM;

sub DumpBlocks()
	var p := freeList;
	var bytes: intptr := 0;
	print("* freelist: ");
	print_hex_i32(p as intptr as uint32);
	print_nl();
	while p != (0 as [MallocFreeBlock]) loop
		print("* block @");
		print_hex_i32(p as intptr as uint32);
		print("+");
		print_hex_i32(p.size as intptr as uint32);
		print("=");
		print_hex_i32((p+p.size) as intptr as uint32);
		print(" -> ");
		print_hex_i32(p.next as intptr as uint32);
		print_nl();

		bytes := bytes + p.size;
		p := p.next;
	end loop;
	print("* free bytes: ");
	print_i32(bytes as intptr as uint32);
	print_nl();
	print_nl();
end sub;

sub CheckMemoryChain()
	var p := freeList;
	while p != (0 as [MallocFreeBlock]) loop
		var next := p.next;
		if next == (0 as [MallocFreeBlock]) then
			break;
		end if;
		if next <= p then
			DumpBlocks();
			print("bad chain: block ");
			print_hex_i32(p as intptr as uint32);
			print(" points at prior block ");
			print_hex_i32(next as intptr as uint32);
			print_nl();
			ExitWithError();
		end if;

		p := p.next;
	end loop;
end sub;

sub Alloc(length: intptr): (block: [uint8])
	var totallength := AlignUp(length + @bytesof MallocUsedBlock);
	if totallength < @bytesof MallocFreeBlock then
		totallength := @bytesof MallocFreeBlock;
	end if;

	var prev: [MallocFreeBlock] := 0 as [MallocFreeBlock];
	var p := freeList;
	loop
		if p == (0 as [MallocFreeBlock]) then
			# Uh-oh --- we've run out of free blocks.
			p := 0 as [MallocFreeBlock];
			print("Out of memory\n");
			ExitWithError();
			break;
		end if;

		var s := p.size;
		if s == totallength then
			# We found a hole of exactly the right size. Unlink and return it.
			if prev != (0 as [MallocFreeBlock]) then
				prev.next := p.next;
			else
				freeList := p.next;
			end if;
			break;
		elseif (s - @bytesof MallocFreeBlock) > totallength then
			# We found a hole bigger than we need. We shrink the hole and return
			# what's left. We make sure to always leave enough space for the
			# MallocFreeBlock structure (which is bigger than the
		    # MallocUsedBlock structure).
			p.size := s - totallength;
			p := p + p.size;
			break;
		end if;

		prev := p;
		p := p.next;
	end loop;

	#print("malloc ");
	#print_hex_i32(p as intptr as uint32);
	#print("+");
	#print_hex_i32(totallength as uint32);
	#print_nl();

	var usedblock := p as [MallocUsedBlock];
	usedblock.size := totallength;
	block := (@next usedblock) as [uint8];

	#print("malloc ");
	#print_hex_i32(block as intptr as uint32);
	#print("+");
	#print_hex_i32(length as uint32);
	#print_nl();

	MemSet(block, 0, length);
	#CheckMemoryChain();
end sub;

sub AddFreeBlock(start: [uint8], length: intptr)
	#print("free ");
	#print_hex_i32(start as intptr as uint32);
	#print("+");
	#print_hex_i32(length as uint32);
	#print_nl();

	var h := start as [MallocFreeBlock];
	h.size := length;

	# freeList points at an ordered list of free blocks. First, we run
	# through the list until we find the last block *before* this one.

	var p := freeList;
	if h < p then
		# Special case: the new block will become the new *first* block.

		if (h + h.size) == p then
			h.size := length + p.size;
			h.next := p.next;
		else
			h.next := p;
		end if;
		freeList := h;
	else
		# Otherwise, work through the list and find the block immediately
		# preceding the one we're going to insert.

		loop
			# Is this the last block?

			if p.next == (0 as [MallocFreeBlock]) then
				# Insert the new block after it.

				p.next := h;
				h.next := 0 as [MallocFreeBlock];
				break;
			end if;

			# We know that h cannot be before p, so if h is before the
			# *next* block, then this must be the block preceding it.

			if h < p.next then
				# Try to merge the next block onto the end of h.

				if (h + length) == p.next then
					h.size := length + p.next.size;
					h.next := p.next.next;
				else
					h.next := p.next;
				end if;
				p.next := h;
				break;
			end if;

			p := p.next;
		end loop;

		# p now points at the preceding block. Try to merge h onto the
		# end of it.

		if (p + p.size) == h then
			p.size := p.size + h.size;
			p.next := h.next;
		end if;
	end if;

	#CheckMemoryChain();
end sub;

sub Free(start: [uint8])
	if start != (0 as [uint8]) then
		var usedblock := @prev (start as [MallocUsedBlock]);
		AddFreeBlock(usedblock as [uint8], usedblock.size);
	end if;
end sub;

sub GetFreeMemory(): (bytes: intptr)
	bytes := 0;
	var p := freeList;
	while p != (0 as [MallocFreeBlock]) loop
		bytes := bytes + p.size;
		p := p.next;
	end loop;
end sub;

sub StrDup(s: [uint8]): (news: [uint8])
	var len := StrLen(s) + 1;
	news := Alloc(len);
	MemCopy(s, len, news);
end sub;

