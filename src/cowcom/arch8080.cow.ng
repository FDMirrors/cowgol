%{
	var uint32_type: [Symbol];
	var int32_type: [Symbol];
	var uint16_type: [Symbol];
	var int16_type: [Symbol];
	var uint8_type: [Symbol];
	var int8_type: [Symbol];
	var intptr_type: [Symbol];

	record Extern
		name: string;
		id: uint16;
		next: [Extern];
	end record;

	var externs: [Extern] := (0 as [Extern]);

	sub ArchAlignUp(value: Size, alignment: uint8): (newvalue: Size)
		newvalue := value;
	end sub;

	sub ArchGuessIntType(min: Arith, max: Arith): (symbol: [Symbol])
		if (min >= 0) and (max <= 255) then
			symbol := uint8_type;
		elseif (min >= -128) and (max <= 127) then
			symbol := int8_type;
		elseif (min >= 0) and (max <= 65535) then
			symbol := uint16_type;
		elseif (min >= -32768) and (max <= 32767) then
			symbol := int16_type;
		elseif (min >= 0) then
			symbol := uint32_type;
		else
			symbol := int32_type;
		end if;
	end sub;

	sub ArchInitVariable(symbol: [Symbol])
		var subr := symbol.vardata.subr;
		var offset := subr.workspace[0];
		symbol.vardata.offset := offset;
		subr.workspace[0] := offset + symbol.vardata.type.typedata.width;
	end sub;

	sub ArchInitMember(containing: [Symbol], member: [Symbol], position: Size)
		member.vardata.offset := position;
		position := position + member.vardata.type.typedata.width;
		if position > containing.typedata.width then
			containing.typedata.width := position;
		end if;
	end sub;

	sub E_labelref(labelid: uint16)
		EmitByte(COO_ESCAPE_THISCOO);
		E_h16(labelid);
	end sub;

	sub E_subref(subr: [Subroutine])
		if subr == current_subr then
			EmitByte(COO_ESCAPE_THISSUB);
		else
			EmitByte(COO_ESCAPE_SUBREF);
			E_h16(subr.id);
		end if;
	end sub;

	sub E_symref(sym: [Symbol], off: Size)
		if sym.vardata.externname != (0 as string) then
			E(sym.vardata.externname);
			EmitByte('+');
			E_u16(off);
		else
			EmitByte(COO_ESCAPE_WSREF);
			E_h16(sym.vardata.subr.id);
			E_h8(0);
			E_h16(sym.vardata.offset + off);
		end if;
	end sub;

	sub E_space()
		EmitByte(' ');
	end sub;

	sub E_comma()
		EmitByte(',');
	end sub;

	sub E_tab()
		EmitByte('\t');
	end sub;

	sub E_nl();
		EmitByte('\n');
	end sub;

	sub R_flushall()
		RegCacheFlush(ALL_REGS);
	end sub;

	sub R_flush(reg: RegId)
		RegCacheFlush(FindConflictingRegisters(reg));
	end sub;

	sub E_label(label: LabelRef)
		R_flushall();
		E_labelref(label);
		E(":\n");
	end sub;

	sub E_jump(insn: string, label: LabelRef)
		R_flushall();
		E_tab();
		E(insn);
		E_space();
		E_labelref(label);
		E_nl();
	end sub;

	sub E_jmp(label: LabelRef)
		E_jump("jmp", label);
	end sub;

	sub E_jnz(label: LabelRef)
		E_jump("jnz", label);
	end sub;

	sub E_call(subr: [Subroutine])
		R_flushall();
		E_tab();
		E("call ");
		E_subref(subr);
		E_nl();
	end sub;

	sub loreg(reg: RegId): (result: RegId)
		case reg is
			when REG_A:  result := REG_A;
			when REG_BC: result := REG_C;
			when REG_DE: result := REG_E;
			when REG_HL: result := REG_L;
			when else:
				SimpleError("bad loreg");
		end case;
	end sub;

	sub hireg(reg: RegId): (result: RegId)
		case reg is
			when REG_A:  result := REG_A;
			when REG_BC: result := REG_B;
			when REG_DE: result := REG_D;
			when REG_HL: result := REG_H;
			when else:
				SimpleError("bad hireg");
		end case;
	end sub;

	sub E_reg(reg: RegId)
		case reg is
			when REG_A: EmitByte('a');
			when REG_B: EmitByte('b');
			when REG_C: EmitByte('c');
			when REG_D: EmitByte('d');
			when REG_E: EmitByte('e');
			when REG_H: EmitByte('h');
			when REG_L: EmitByte('l');
			when REG_BC: EmitByte('b');
			when REG_DE: EmitByte('d');
			when REG_HL: EmitByte('h');
			when else:
				StartError();
				print("invalid register 0x");
				print_hex_i16(reg);
				EndError();
		end case;
	end sub;

	sub E_stackreg(reg: RegId)
		if reg == REG_A then
			E("psw");
		else
			E_reg(reg);
		end if;
	end sub;

	sub E_ret()
		E("\tret\n");
	end sub;

	sub E_mov(dest: RegId, src: RegId)
		R_flush(dest);
		E_tab();
		E("mov ");
		E_reg(dest);
		E_comma();
		E_reg(src);
		E_nl();
	end sub;

	sub E_push(src: RegId)
		E_tab();
		E("push ");
		E_stackreg(src);
		E_nl();
	end sub;

	sub E_pop(dest: RegId)
		R_flush(dest);
		E_tab();
		E("pop ");
		E_stackreg(dest);
		E_nl();
	end sub;

	sub E_mvi(reg: RegId, value: uint8)
		if (reg & (REG_HL|REG_BC|REG_DE)) != 0 then
			SimpleError("mvi with 16-bit reg");
		end if;

		var cache := RegCacheFindConstant(value as CacheValue)
			& (REG_A|REG_B|REG_C|REG_D|REG_H|REG_L);
		if (cache & reg) != 0 then
			# Already in the desired register.
			return;
		elseif cache != 0 then
			# Already in a register, but not the one we want.
			E_mov(reg, FindFirst(cache));
			return;
		end if;

		R_flush(reg);
		E("\tmvi ");
		E_reg(reg);
		E_comma();
		E_u8(value);
		E_nl();
		RegCacheLeavesConstant(reg, value as CacheValue);
	end sub;

	sub E_alu(insn: string, rhs: RegId)
		R_flush(REG_A);
		E_tab();
		E(insn);
		E_space();
		E_reg(rhs);
		E_nl();
	end sub;

	sub E_xra(rhs: RegId)
		E_alu("xra", rhs);
		if rhs == REG_A then
			RegCacheLeavesConstant(REG_A, 0);
		end if;
	end sub;

	sub E_ora(rhs: RegId)
		E_alu("ora", rhs);
	end sub;

	sub E_ana(rhs: RegId)
		E_alu("ana", rhs);
	end sub;

	sub E_add(rhs: RegId)
		E_alu("add", rhs);
	end sub;

	sub E_sub(rhs: RegId)
		E_alu("sub", rhs);
	end sub;

	sub E_sbb(rhs: RegId)
		E_alu("sbb", rhs);
	end sub;

	sub E_alui(insn: string, value: uint8)
		R_flush(REG_A);
		E_tab();
		E(insn);
		E_space();
		E_u8(value);
		E_nl();
	end sub;

	sub E_cmp(reg: RegId);
		E_alu("cmp", reg);
	end sub;

	sub E_cpi(value: uint8)
		E_alui("cpi", value);
	end sub;

	sub E_adi(value: uint8)
		E_alui("adi", value);
	end sub;

	sub E_sui(value: uint8)
		E_alui("sui", value);
	end sub;

	sub E_sbi(value: uint8)
		E_alui("sbi", value);
	end sub;

	sub E_ori(value: uint8)
		E_alui("ori", value);
	end sub;

	sub E_ani(value: uint8)
		E_alui("ani", value);
	end sub;

	sub E_xri(value: uint8)
		E_alui("xri", value);
	end sub;

	sub E_lxi(reg: RegId, value: uint16);
		var cache := RegCacheFindConstant(value as CacheValue) & (REG_HL|REG_BC|REG_DE);
		if (cache & reg) != 0 then
			# The value is already in the desired register.
			return;
		elseif cache != 0 then
			# The value is already in a register, but not this one.
			cache := FindFirst(cache);
			E_mov(loreg(reg), loreg(cache));
			E_mov(hireg(reg), hireg(cache));
			return;
		end if;

		R_flush(reg);
		E("\tlxi ");
		E_reg(reg);
		E_comma();
		E_u16(value);
		E_nl();
		RegCacheLeavesConstant(reg, value as CacheValue);
		RegCacheLeavesConstant(loreg(reg), value as uint8 as CacheValue);
		RegCacheLeavesConstant(hireg(reg), (value>>8) as uint8 as CacheValue);
	end sub;

	sub E_lxia(reg: RegId, sym: [Symbol], off: Size)
		# This optimisation is essentially not worth it on the 8080.
		#var cache := RegCacheFindAddress(sym, off) & (REG_HL|REG_BC|REG_DE);
		#if (cache & reg) != 0 then
		#	# The value is already in the desired register.
		#	return;
		#elseif cache != 0 then
		#	# The value is already in a register, but not this one.
		#	cache := FindFirst(cache);
		#	E_mov(loreg(reg), loreg(cache));
		#	E_mov(hireg(reg), hireg(cache));
		#	return;
		#end if;

		R_flush(reg);
		E("\tlxi ");
		E_reg(reg);
		E_comma();
		E_symref(sym, off);
		E_nl();
		#RegCacheLeavesAddress(reg, sym, off);
	end sub;

	sub E_lda(sym: [Symbol], off: Size)
		var cache := RegCacheFindValue(sym, off);
		if (cache & REG_A) != 0 then
			# Value already in the right register.
			return;
		end if; # Other cases don't happen on the 8080.

		R_flush(REG_A);
		E("\tlda ");
		E_symref(sym, off);
		E_nl();
		RegCacheLeavesValue(REG_A, sym, off);
	end sub;

	sub E_sta(sym: [Symbol], off: Size)
		E("\tsta ");
		E_symref(sym, off);
		E_nl();
		RegCacheLeavesValue(REG_A, sym, off);
	end sub;

	sub E_lhld(sym: [Symbol], off: Size)
		var cache := RegCacheFindValue(sym, off);
		if (cache & REG_HL) != 0 then
			# Value already in the right register.
			return;
		end if; # Other cases don't happen on the 8080.

		R_flush(REG_HL);
		E("\tlhld ");
		E_symref(sym, off);
		E_nl();
		RegCacheLeavesValue(REG_HL, sym, off);
	end sub;

	sub E_shld(sym: [Symbol], off: Size)
		E("\tshld ");
		E_symref(sym, off);
		E_nl();
		RegCacheLeavesValue(REG_HL, sym, off);
	end sub;

	sub E_loadm(reg: RegId)
		R_flush(reg);
		E("\tmov ");
		E_reg(reg);
		E(",m\n");
	end sub;

	sub E_storem(reg: RegId)
		RegCacheFlushValues();
		E("\tmov m,");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_stax(ptr: RegId)
		RegCacheFlushValues();
		E("\tstax ");
		E_reg(ptr);
		E_nl();
		RegCacheFlushValues();
	end sub;

	sub E_ldax(ptr: RegId)
		RegCacheFlushValues();
		R_flush(REG_A);
		E("\tldax ");
		E_reg(ptr);
		E_nl();
	end sub;

	sub E_inc(reg: RegId)
		R_flush(reg);
		E("\tinc ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_dec(reg: RegId)
		R_flush(reg);
		E("\tdec ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_inx(reg: RegId)
		R_flush(reg);
		E("\tinx ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_dcx(reg: RegId)
		R_flush(reg);
		E("\tdcx ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_xchg()
		R_flush(REG_HL|REG_DE);
		E("\txchg\n");
	end sub;

	sub E_pchl()
		R_flushall();
		E("\tpchl\n");
	end sub;

	sub E_cma()
		R_flush(REG_A);
		E("\tcma\n");
	end sub;

	sub E_ral()
		R_flush(REG_A);
		E("\tral\n");
	end sub;

	sub E_rar()
		R_flush(REG_A);
		E("\trar\n");
	end sub;

	sub E_dad(reg: RegId)
		R_flush(REG_HL);
		E("\tdad ");
		E_reg(reg);
		E_nl();
	end sub;

	# Does not persist the name; only call this with constant strings.
	sub E_callhelper(name: string)
		var e := externs;
		while e != (0 as [Extern]) loop
			if StrCmp(e.name, name) == 0 then
				break;
			end if;
			e := e.next;
		end loop;

		if e == (0 as [Extern]) then
			e := Alloc(@bytesof Extern) as [Extern];
			e.name := name;
			e.id := AllocSubrId();
			e.next := externs;
			externs := e;

			EmitterDeclareExternalSubroutine(e.id, name);
		end if;

		EmitterPushChunk();
		E_h16(current_subr.id);
		E_h16(e.id);
		EmitterPopChunk('R');

		R_flushall();
		E("\tcall ");
		EmitByte(COO_ESCAPE_SUBREF);
		E_h16(e.id);
		E_nl();
	end sub;

	var stringid: uint16 := 0;

	sub E_string(data: string)
		var sid := stringid;
		stringid := stringid + 1;

		EmitterPushChunk();
		E_h16(current_subr.id);

		E("\tcseg\n");
		EmitByte(COO_ESCAPE_THISCOO);
		EmitByte('c');
		E_u16(sid);
		E(":\n\tdb ");

		loop
			var c := [data];
			if c == 0 then
				break;
			end if;
			data := data + 1;
			
			E_u8(c);
			E_comma();
		end loop;
		E("0\n");
		EmitterPopChunk('S');

		EmitByte(COO_ESCAPE_THISCOO);
		EmitByte('c');
		E_u16(sid);
	end sub;

	# Note that this *destroys* the source register.
	sub ArchEmitMove(src: RegId, dest: RegId)
		if src == 0 then
			E_pop(dest);
		elseif dest == 0 then
			E_push(src);
		elseif ((src == REG_HL) and (dest == REG_DE))
				or ((src == REG_DE) and (dest == REG_HL)) then
			E_xchg();
		else
			E_mov(dest, src);

			if (src & (REG_HL|REG_BC|REG_DE)) != 0 then
				E_mov(loreg(dest), loreg(src));
			end if;
		end if;
	end sub;

	sub ArchEndInstruction()
	end sub;
%}

width 16;

register a b c d e h l hl de bc;
register stk4 param;

regdata a compatible a|b|d|h;
regdata b uses bc compatible a|b|d|h;
regdata d uses de compatible a|b|d|h;
regdata h uses hl compatible a|b|d|h;
regdata bc uses bc compatible bc|de|hl;
regdata de uses de compatible bc|de|hl;
regdata hl uses hl compatible bc|de|hl;
regdata stk4 stacked;
regdata param stacked;

// --- Core things ----------------------------------------------------------

gen STARTFILE();
gen ENDFILE();

gen LABEL():b
{
	E_label($b.label);
}

gen JUMP():j
{
	E_jmp($j.label);
}

// --- Subroutines ----------------------------------------------------------

gen STARTSUB():s
{
	RegCacheReset();

	EmitterPushChunk();
	E_h16($s.subr.id);

	E("\n\n\t; ");
	E($s.subr.name);
	E_nl();

	E("\tcseg\n");
	EmitByte(COO_ESCAPE_THISSUB);
	E(":\n");

	var count := $s.subr.num_input_parameters;
	var lastparam := count - 1;
	var popped: uint8 := 0;

	sub pop_return_address()
		if popped == 0 then
			E_pop(REG_BC);
			popped := 1;
		end if;
	end sub;

	while count != 0 loop
		count := count - 1;
		var param := GetInputParameter($s.subr, count);

		case param.vardata.type.typedata.width is
			when 1:
				if count != lastparam then
					pop_return_address();
					E_pop(REG_A);
				end if;
				E_sta(param, 0);

			when 2:
				if count != lastparam then
					pop_return_address();
					E_pop(REG_HL);
				end if;
				E_shld(param, 0);

			when 4:
				pop_return_address();
				E_pop(REG_HL);
				E_shld(param, 0);
				E_pop(REG_HL);
				E_shld(param, 2);
		end case;
	end loop;

	if popped != 0 then
		E_push(REG_BC);
	end if;
}

gen ENDSUB():s
{
	R_flushall();

	E("end_");
	E_subref($s.subr);
	E(":\n");

	var count: uint8 := 0;
	var params := $s.subr.num_output_parameters;
	var pushed: uint8 := 0;

	sub push_return_address()
		if pushed == 0 then
			E_pop(REG_BC);
			pushed := 1;
		end if;
	end sub;

	while count != params loop
		var param := GetOutputParameter($s.subr, count);

		case param.vardata.type.typedata.width is
			when 1:
				E_lda(param, 0);
				if count != (params-1) then
					push_return_address();
					E_push(REG_A);
				end if;

			when 2:
				E_lhld(param, 0);
				if count != (params-1) then
					push_return_address();
					E_push(REG_HL);
				end if;

			when 4:
				push_return_address();
				E_lhld(param, 2);
				E_push(REG_HL);
				E_lhld(param, 0);
				E_push(REG_HL);
		end case;

		count := count + 1;
	end loop;

	if pushed != 0 then
		E_push(REG_BC);
	end if;
	E_ret();

	EmitterDeclareWorkspace($s.subr, 0, $s.subr.workspace[0]);
	EmitterPopChunk('S');
}

gen CALL():s
		{ E_call($s.subr); }
	
gen a := CALLE1():s
		{ E_call($s.subr); }

gen hl := CALLE2():s
		{ E_call($s.subr); }

gen stk4 := CALLE4():s
		{ E_call($s.subr); }

gen PUSHARG1(a, remaining==0);

gen PUSHARG1(a|b|d|h:lhs, remaining!=0)
		{ E_push($lhs); }
	
gen PUSHARG2(hl, remaining==0);

gen PUSHARG2(bc|de|hl:lhs, remaining!=0)
		{ E_push($lhs); }

gen PUSHARG2(CALLE2():s)
		{ E_call($s.subr); }

gen PUSHARG4(stk4); // already stacked

gen PUSHARG4(CALLE4():s)
		{ E_call($s.subr); }

gen a := POPARG1(remaining==0);

gen a|b|d|h := POPARG1(remaining!=0)
		{ E_pop(REG_A); }

gen hl := POPARG2(remaining==0);

gen hl|bc|de := POPARG2()
		{ E_pop(REG_HL); }

gen stk4 := POPARG4();

gen RETURN()
{
    if current_subr.num_output_parameters == 0 then
		E_ret();
	else
		E("\tjmp end_");
		E_subref(current_subr);
		E("\n");
	end if;
}

// --- Constants ------------------------------------------------------------

gen a|b|d|h := CONSTANT():rhs
{
	if ($rhs.value == 0) and ($$ == REG_A) then
		E_xra(REG_A);
	else
		E_mvi($$, $rhs.value as uint8);
	end if;
}

gen bc|de|hl := CONSTANT():rhs
{
	E_lxi($$, $rhs.value as uint16);
}

gen stk4 := CONSTANT():rhs uses hl
{
	E_lxi(REG_HL, ($rhs.value >> 16) as uint16);
	E_push(REG_HL);
	E_lxi(REG_HL, $rhs.value as uint16);
	E_push(REG_HL);
}

gen bc|de|hl := ADDRESS():a
{
	E_lxia($$, $a.sym, $a.off);
}

// --- 8-bit loads and stores -----------------------------------------------

gen a := LOAD1(ADDRESS():a)
{
	E_lda($a.sym, $a.off);
}

gen a := LOAD1(bc|de|hl:ptr)
{
	if $ptr == REG_HL then
		E_loadm(REG_A);
	else
		E_ldax($ptr);
	end if;
}

gen STORE1(a, ADDRESS():a)
{
	E_sta($a.sym, $a.off);
}

gen STORE1(a, bc|de|hl:ptr)
{
	if $ptr == REG_HL then
		E_storem(REG_A);
	else
		E_stax($ptr);
	end if;
}

// --- 16-bit loads and stores ----------------------------------------------

gen hl := LOAD2(ADDRESS():a)
{
	E_lhld($a.sym, $a.off);
}

%{
	sub load2(dest: RegId)
		if dest == REG_HL then
			E_loadm(REG_A);
			E_inx(REG_HL);
			E_loadm(dest);
			E_mov(loreg(dest), REG_A);
		else
			E_loadm(loreg(dest));
			E_inx(REG_HL);
			E_loadm(dest);
		end if;
	end sub;
%}

gen hl|bc|de := LOAD2(hl) uses a
{
	load2($$);
}

gen STORE2(hl, ADDRESS():a)
{
	E_shld($a.sym, $a.off);
}

gen STORE2(bc|de:val, hl) uses a
{
	E_storem(loreg($val));
	E_inx(REG_HL);
	E_storem($val);
}

// --- 32-bit loads and stores ----------------------------------------------

gen stk4 := LOAD4(hl)
		{ E_callhelper("_load4"); }

gen STORE4(stk4, hl)
		{ E_callhelper("_store4"); }

// --- 8-bit arithmetic -----------------------------------------------------

gen a := ADD1(a, b|d|h:rhs)
		{ E_add($rhs); }

gen a|b|d|h := ADD1($$, CONSTANT(value==-1))
		{ E_dec($$); }

gen a|b|d|h := ADD1($$, CONSTANT(value==1))
		{ E_inc($$); }

gen a := ADD1(a, CONSTANT():c)
		{ E_adi($c.value as uint8); }

gen a := SUB1(a, b|d|h:rhs)
		{ E_sub($rhs); }

gen a|b|d|h := SUB1($$, CONSTANT(value==1))
		{ E_dec($$); }

gen a := SUB1(a, CONSTANT():c)
		{ E_sbi($c.value as uint8); }

gen a := MUL1(b, d)
		{ E_callhelper("_mul1"); }

gen a := NOT1(a)
		{ E_cma(); }

gen a := NEG1(b|d|h:lhs)
{
	E_xra(REG_A);
	E_sub($lhs);
}

gen a := OR1(a, b|d|h:lhs)
		{ E_ora($lhs); }

gen a := OR1(a, CONSTANT():c)
		{ E_ori($c.value as uint8); }

gen a := AND1(a, b|d|h:lhs)
		{ E_ana($lhs); }

gen a := AND1(a, CONSTANT():c)
		{ E_ani($c.value as uint8); }

gen a := EOR1(a, b|d|h:lhs)
		{ E_xra($lhs); }

gen a := EOR1(a, CONSTANT():c)
		{ E_xri($c.value as uint8); }

gen b := DIVU1(b, d) uses a
		{ E_callhelper("_dvrmu1"); }

gen a := REMU1(b, d)
		{ E_callhelper("_dvrmu1"); }

gen b := DIVS1(b, d) uses a
		{ E_callhelper("_dvrms1"); }

gen d := REMS1(b, d)
		{ E_callhelper("_dvrms1"); }

gen a := RSHIFTS1(a, b) uses bc
		{ E_callhelper("_asr1"); }

gen a := RSHIFTU1(a, b) uses bc
		{ E_callhelper("_lsr1"); }

gen a := LSHIFT1(a, b) uses bc
		{ E_callhelper("_asl1"); }

gen a := LSHIFT1(a, CONSTANT(value<=5):c)
{
	var i := $c.value as uint8;
	while i != 0 loop
		E_add(REG_A);
		i := i - 1;
	end loop;
}

gen a := RSHIFTU1(a, CONSTANT(value<=2):c)
{
	var i := $c.value as uint8;
	while i != 0 loop
		E_ora(REG_A);
		E_rar();
		i := i - 1;
	end loop;
}

// --- 16-bit arithmetic ----------------------------------------------------

%{
	sub is_small_positive(value: int32): (result: uint8)
		if (value > 0) and (value < 5) then
			result := 1;
		else
			result := 0;
		end if;
	end sub;

	sub is_small_negative(value: int32): (result: uint8)
		if (value < 0) and (value > -5) then
			result := 1;
		else
			result := 0;
		end if;
	end sub;
%}
		
gen hl|bc|de := ADD2($$, CONSTANT(value is small_positive):c)
{
	var i: uint8 := $c.value as uint8;
	while i != 0 loop
		E_inx($$);
		i := i - 1;
	end loop;
}

gen hl|bc|de := ADD2($$, CONSTANT(value is small_negative):c)
{
	var i: uint8 := $c.value as uint8;
	while i != 0 loop
		E_dcx($$);
		i := i + 1;
	end loop;
}

gen hl := ADD2(hl|bc|de:lhs, hl|bc|de:rhs)
{
	if ($lhs != REG_HL) and ($rhs != REG_HL) then
		if $rhs == REG_DE then
			E_xchg();
			$rhs := REG_HL;
		else
			if $lhs == REG_DE then
				E_xchg();
			else
				ArchEmitMove($lhs, REG_HL);
			end if;
			$lhs := REG_HL;
		end if;
	end if;

	if $lhs == REG_HL then
		E_dad($rhs);
	else
		E_dad($lhs);
	end if;
}

gen bc|de|hl := NEG2(bc|de|hl:lhs) uses a
{
	E_xra(REG_A);
	E_sub(loreg($lhs));
	E_mov(loreg($$), REG_A);
	E_sbb(REG_A);
	E_sub($lhs);
	E_mov($$, REG_A);
}

%{
	sub aluop2(lhs: RegId, rhs: RegId, dest: RegId, loinsn: string, hiinsn: string)
		E_mov(REG_A, loreg(lhs));
		E_alu(loinsn, loreg(rhs));
		E_mov(loreg(dest), REG_A);
		E_mov(REG_A, lhs);
		E_alu(hiinsn, rhs);
		E_mov(dest, REG_A);
	end sub;

	sub aluop2i(lhs: RegId, value: uint16, dest: RegId, loinsn: string, hiinsn: string)
		E_mov(REG_A, loreg(lhs));
		E_alui(loinsn, value as uint8);
		E_mov(loreg(dest), REG_A);
		E_mov(REG_A, lhs);
		E_alui(hiinsn, (value >> 8) as uint8);
		E_mov(dest, REG_A);
	end sub;
%}

gen bc|de|hl := SUB2(bc|de|hl:lhs, bc|de|hl:rhs) uses a
{
	aluop2($lhs, $rhs, $$, "sub", "sbb");
}

gen bc|de|hl := SUB2(bc|de|hl:lhs, CONSTANT():c) uses a
{
	aluop2i($lhs, $c.value as uint16, $$, "sui", "sbi");
}

gen de := DIVU2(de, bc) uses a
		{ E_callhelper("_dvrmu2"); }

gen hl := REMU2(de, bc) uses a
		{ E_callhelper("_dvrmu2"); }

gen de := DIVS2(de, bc) uses a
		{ E_callhelper("_dvrms2"); }

gen hl := REMS2(de, bc) uses a
		{ E_callhelper("_dvrms2"); }

gen hl := MUL2(hl, de) uses a|bc
		{ E_callhelper("_mul2"); }

%{
	sub logic2(lhs: RegId, rhs: RegId, dest: RegId, insn: string)
		aluop2(lhs, rhs, dest, insn, insn);
	end sub;

	sub logic2i(lhs: RegId, value: uint16, dest: RegId, insn: string)
		aluop2i(lhs, value, dest, insn, insn);
	end sub;
%}

gen bc|de|hl := OR2(bc|de|hl:lhs, bc|de|hl:rhs) uses a
		{ logic2($lhs, $rhs, $$, "ora"); }

gen bc|de|hl := OR2(bc|de|hl:lhs, CONSTANT():c) uses a
		{ logic2i($lhs, $c.value as uint16, $$, "ori"); }

gen bc|de|hl := AND2(bc|de|hl:lhs, bc|de|hl:rhs) uses a
		{ logic2($lhs, $rhs, $$, "ana"); }

gen bc|de|hl := AND2(bc|de|hl:lhs, CONSTANT():c) uses a
		{ logic2i($lhs, $c.value as uint16, $$, "ani"); }

gen bc|de|hl := EOR2(bc|de|hl:lhs, bc|de|hl:rhs) uses a
		{ logic2($lhs, $rhs, $$, "xra"); }

gen bc|de|hl := EOR2(bc|de|hl:lhs, CONSTANT():c) uses a
		{ logic2i($lhs, $c.value as uint16, $$, "xri"); }

gen bc|de|hl := NOT2(bc|de|hl:lhs) uses a
{
	E_mov(REG_A, loreg($lhs));
	E_cma();
	E_mov(loreg($$), REG_A);
	E_mov(REG_A, $lhs);
	E_cma();
	E_mov($$, REG_A);
}

gen hl := RSHIFTU2(hl, b) uses a
		{ E_callhelper("_lsr2"); }

gen hl := RSHIFTS2(hl, b) uses a
		{ E_callhelper("_asr2"); }

gen hl := LSHIFT2(hl, b) uses a
		{ E_callhelper("_asl2"); }

gen hl := LSHIFT2(hl, CONSTANT(value<=5):c)
{
	var i := $c.value as uint8;
	while i != 0 loop
		E_dad(REG_HL);
		i := i - 1;
	end loop;
}

// --- 32-bit arithmetic ----------------------------------------------------

gen stk4 := ADD4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_add4"); }

gen stk4 := SUB4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_sub4"); }

gen stk4 := NEG4(stk4) uses a|bc|de|hl
        { E_callhelper("_neg4"); }

gen stk4 := MUL4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_mul4"); }

gen stk4 := DIVU4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_divu4"); }

gen stk4 := REMU4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_remu4"); }

gen stk4 := DIVS4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_divs4"); }

gen stk4 := REMS4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_rems4"); }

gen stk4 := AND4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_and4"); }

gen stk4 := OR4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_or4"); }

gen stk4 := EOR4(stk4, stk4) uses a|bc|de|hl
        { E_callhelper("_eor4"); }

gen stk4 := NOT4(stk4) uses a|bc|de|hl
        { E_callhelper("_not4"); }

gen stk4 := LSHIFT4(stk4, b) uses a|de|hl
        { E_callhelper("_asl4"); }

gen stk4 := RSHIFTU4(stk4, b) uses a|de|hl
        { E_callhelper("_lsr4"); }

gen stk4 := RSHIFTS4(stk4, b) uses a|de|hl
        { E_callhelper("_asr4"); }

// --- Conditionals ---------------------------------------------------------

%{
	sub E_jumps_with_fallthrough(trueinsn: string, falseinsn: string, node: [Node])
		if node.beqs0.truelabel != node.beqs0.fallthrough then
			E_jump(trueinsn, node.beqs0.truelabel);
		end if;
		if node.beqs0.falselabel != node.beqs0.fallthrough then
			E_jump(falseinsn, node.beqs0.falselabel);
		end if;
	end sub;

	sub E_jumps_jz_jnz(node: [Node])
		E_jumps_with_fallthrough("jz", "jnz", node);
	end sub;

	sub E_jumps_jc_jnc(node: [Node])
		E_jumps_with_fallthrough("jc", "jnc", node);
	end sub;

	sub bequ1(node: [Node], nota: RegId)
		E_cmp(nota);
		E_jumps_jz_jnz(node);
	end sub;

	sub bequ1c(node: [Node], value: uint8)
		if value == 0 then
			E_ora(REG_A);
		else
			E_cpi(value);
		end if;
		E_jumps_jz_jnz(node);
	end sub;

	sub bequ2(node: [Node])
		E_callhelper("_cmpeq2");
		E_jumps_jz_jnz(node);
	end sub;

	sub bequ2c(node: [Node], value: uint16)
		if value != 0 then
			E_lxi(REG_DE, -value);
			E_dad(REG_DE);
		end if;
		E_mov(REG_A, REG_H);
		E_ora(REG_L);
		E_jumps_jz_jnz(node);
	end sub;

	sub bequ4(node: [Node])
		E_callhelper("_cmpu4");
		E_jumps_jz_jnz(node);
	end sub;

	sub beqc(node: [Node], lhs: Arith, rhs: Arith)
		var label := node.beqs0.falselabel;
		if lhs == rhs then
			label := node.beqs0.truelabel;
		end if;
		if label != node.beqs0.fallthrough then
			E_jmp(label);
		end if;
	end sub;
%}

gen BEQU0(CONSTANT():c1, CONSTANT():c2):b
		{ beqc(self.n[0], $c1.value, $c2.value); }

gen BEQS0(CONSTANT():c1, CONSTANT():c2):b
		{ beqc(self.n[0], $c1.value, $c2.value); }

gen BEQU1(b|d|h:nota, a):b
		{ bequ1(self.n[0], $nota); }

gen BEQU1(a, CONSTANT():c):b
		{ bequ1c(self.n[0], $c.value as uint8); }

gen BEQS1(b|d|h:nota, a):b
		{ bequ1(self.n[0], $nota); }

gen BEQS1(a, CONSTANT():c):b
		{ bequ1c(self.n[0], $c.value as uint8); }

gen BLTU1(a, b|d|h:rhs):b
{
	E_cmp($rhs);
	E_jumps_jc_jnc(self.n[0]);
}

gen BLTU1(a, CONSTANT():c):b
{
	var v := $c.value as uint8;
	if v == 0 then
		E_ora(REG_A);
	else
		E_cpi(v);
	end if;
	E_jumps_jc_jnc(self.n[0]);
}

gen BLTS1(a, b):b
{
	E_callhelper("_cmps1");
	E_jumps_with_fallthrough("jm", "jp", self.n[0]);
}

gen BEQU2(de, hl):a
		{ bequ2(self.n[0]); }

gen BEQU2(hl, CONSTANT():c) uses a|de
		{ bequ2c(self.n[0], $c.value as uint16); }

gen BEQS2(de, hl):a
		{ bequ2(self.n[0]); }

gen BEQS2(hl, CONSTANT():c) uses a|de
		{ bequ2c(self.n[0], $c.value as uint16); }

gen BLTU2(hl|bc|de:lhs, hl|bc|de:rhs):b uses a
{
	E_mov(REG_A, loreg($lhs));
	E_sub(loreg($rhs));

	E_mov(REG_A, $lhs);
	E_sbb($rhs);

	E_jumps_jc_jnc(self.n[0]);
}

gen BLTU2(hl|bc|de:lhs, CONSTANT():c):b uses a
{
	E_mov(REG_A, loreg($lhs));
	E_sui($c.value as uint8);

	E_mov(REG_A, $lhs);
	E_sbi((($c.value as uint16) >> 8) as uint8);

	E_jumps_jc_jnc(self.n[0]);
}

gen BLTS2(de, hl):b uses a|bc
{
	E_callhelper("_cmps2");
	E_jumps_jc_jnc(self.n[0]);
}

gen BEQU4(stk4:lhs, stk4:rhs) uses a|hl|bc|de
		{ bequ4(self.n[0]); }

gen BEQS4(stk4:lhs, stk4:rhs) uses a|hl|bc|de
		{ bequ4(self.n[0]); }

gen BLTU4(stk4:lhs, stk4:rhs):b uses a|hl|bc|de
{
	E_callhelper("_cmpu4");
	E_jumps_jc_jnc(self.n[0]);
}

gen BLTS4(stk4:lhs, stk4:rhs):b uses a|hl|bc|de
{
	E_callhelper("_cmps4");
	E_jumps_jc_jnc(self.n[0]);
}

// --- Case -----------------------------------------------------------------

gen STARTCASE1(a);

gen STARTCASE2(de);

gen STARTCASE4(stk4)
{
	E_pop(REG_BC);
	E_pop(REG_DE);
}

gen WHENCASE1():c
{
	if $c.value == 0 then
		E_ora(REG_A);
	else
		E_cpi($c.value as uint8);
	end if;
	E_jnz($c.falselabel);
}

%{
	sub case2(value: uint16, reg: RegId, label: LabelRef)
		if value < 0x100 then
			E_mov(REG_A, loreg(reg));
			if value != 0 then
				E_sbi(value as uint8);
			end if;
			E_ora(hireg(reg));
		else
			E_lxi(REG_HL, -value);
			E_dad(reg);
			E_mov(REG_A, REG_H);
			E_ora(REG_L);
		end if;
		E_jnz(label);
	end sub;
%}

gen WHENCASE2():c uses a
{
	case2($c.value as uint16, REG_DE, $c.falselabel);
}

gen WHENCASE4():c uses a
{
	case2($c.value as uint16, REG_BC, $c.falselabel);
	case2(($c.value >> 16) as uint16, REG_DE, $c.falselabel);
}

// --- Casts ----------------------------------------------------------------

gen hl|bc|de := CAST12(a, sext==0)
{
	E_mov(loreg($$), REG_A);
	E_mvi(hireg($$), 0);
}

gen hl|bc|de := CAST12(a, sext!=0)
{
	E_mov(loreg($$), REG_A);
	E_ral();
	E_sbb(REG_A);
	E_mov($$, REG_A);
}

gen stk4 := CAST14(a:rhs, sext==0) uses hl
{
	E_lxi(REG_HL, 0);
	E_push(REG_HL);
	E_mov(REG_L, REG_A);
	E_push(REG_HL);
}

gen stk4 := CAST14(a:rhs, sext!=0) uses hl|b
{
	E_mov(REG_B, REG_A);
	E_ral();
	E_sbb(REG_A);
	E_mov(REG_H, REG_A);
	E_mov(REG_L, REG_A);
	E_push(REG_HL);
	E_mov(REG_L, REG_B);
	E_push(REG_HL);
}

gen a := CAST21(hl|bc|de:rhs)
		{ E_mov(REG_A, loreg($rhs)); }

gen a := CAST21(ADDRESS():a)
		{ E_lda($a.sym, $a.off); }

gen stk4 := CAST24(hl|de:rhs, sext==0) uses bc
{
	E_lxi(REG_BC, 0);
	ArchEmitMove(REG_BC, 0);
	ArchEmitMove(REG_HL, 0);
}

gen stk4 := CAST24(hl|de:rhs, sext!=0) uses a|bc
{
	E_mov(REG_A, $rhs);
	E_ral();
	E_sbb(REG_A);
	E_mov(REG_B, REG_A);
	E_mov(REG_C, REG_A);
	E_push(REG_BC);
	E_push($rhs);
}

gen a := CAST41(stk4) uses hl
{
	E_pop(REG_HL);
	E_mov(REG_A, REG_L);
	E_pop(REG_HL);
}

gen a := CAST41(LOAD4(ADDRESS():a))
		{ E_lda($a.sym, $a.off); }

gen a|b|d|h := CAST41(LOAD4(hl))
		{ E_loadm($$); }

gen hl := CAST42(stk4) uses bc
{
	E_pop(REG_HL);
	E_pop(REG_BC);
}

gen hl := CAST42(LOAD4(ADDRESS():a))
		{ E_lhld($a.sym, $a.off); }
	
gen hl|bc|de := CAST42(LOAD4(hl))
		{ load2($$); }
	
// --- Strings --------------------------------------------------------------

gen bc|de|hl := STRING():s
{
	R_flush($$);
	E("\tlxi ");
	E_reg($$);
	E_comma();
	E_string($s.text);
	E_nl();
}

// --- Initialisers ---------------------------------------------------------

gen STARTINIT():s
{
	EmitterPushChunk();
	E_h16(current_subr.id);
	E("\tcseg\n"); # yes, really
	E($s.sym.vardata.externname);
	E(":\n");
}

gen ENDINIT()
{
	EmitterPopChunk('S');
}

gen INIT1():c
{
	E("\tdb ");
	E_u8($c.value as uint8);
	E_nl();
}

gen INIT2():c
{
	E("\tdw ");
	E_u16($c.value as uint16);
	E_nl();
}

gen INIT4():c
{
	E("\tdword ");
	E_u32($c.value as uint32);
	E_nl();
}

gen INITS():s
{
	E("\tdw ");
	E_string($s.text);
	E_nl();
}

// --- Inline assembly ------------------------------------------------------

gen ASMSTART()
{
	R_flushall();
	E_tab();
}

gen ASMTEXT():t
{
	E($t.text);
	E_space();
}

gen ASMSYMBOL():s
{
	if $s.sym.kind == VAR then
		E_symref($s.sym, 0);
	else
		E_subref($s.sym.subr);
	end if;
	E_space();
}

gen ASMEND()
{
    E_nl();
}


