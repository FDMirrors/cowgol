var current_arg: int8 := 1;
var current_filename: [int8];
var current_filename_string_id: uint16;
var current_line_number: int16;
var current_fd: int8 := -1;
var current_byte: int8 := -1;
var peeked_byte: int8 := -1;

sub next_byte()
    current_byte := peeked_byte;

    loop
        if current_fd != -1 then
            peeked_byte := file_getchar(current_fd);
            if peeked_byte == -1 then
                file_close(current_fd);
                current_fd := -1;
                peeked_byte := 10;
            elseif peeked_byte == 10 then
                current_line_number := current_line_number + 1;
            end if;
            return;
        end if;

        current_filename := ARGV[current_arg];
        if current_filename != 0 then
            current_filename_string_id := lookup_string(
                current_filename, string_length(current_filename));
            current_arg := current_arg + 1;
            current_fd := file_openin(current_filename);
            current_line_number := 1;
        end if;

        if current_fd == -1 then
            peeked_byte := -1;
            return;
        end if;
    end loop;
end sub;

sub error_with_location()
    print(", at about line ");
    print_i16(current_line_number);
    print(" of ");
    if current_filename != 0 then
        print(current_filename);
    else
        print("?");
    end if;
    print_newline();
    save_string_table("strings.dat");
    exit(1);
end sub;

sub print_buffer()
    var i: int8 := 0;
    while i < buffer_index loop
        print_char(input_buffer[i]);
        i := i + 1;
    end loop;
end sub;
