load_thing_table("things.dat");
init_iop_reader(ARGV[1]);

sub print_identifier(identifier_id: uint16)
    var identifier: [IdentifierThing] := find_thing(identifier_id) as [IdentifierThing];
    show_string(identifier.string_id);
end sub;

sub print_label(label_id: uint16)
    loop
        print_hex_i16(label_id);

        var label: [LabelThing] := find_thing(label_id) as [LabelThing];
        if label.thing_type == THING_LABEL then
            break;
        end if;

        label_id := label.target;
        print(" -> 0x");
    end loop;
end sub;

sub do_filename(iop: [FilenameIop])
    print("FILENAME ");
    print_identifier(iop.thing_id);
end sub;

sub do_linenumber(iop: [LinenumberIop])
    print("LINENUMBER ");
    print_i16(iop.line_number);
end sub;

sub do_label(iop: [LabelIop])
    print("LABEL 0x");
    print_label(iop.label_id);
end sub;

sub do_thing(iop: [ThingIop])
    print("THING id=0x");
    print_hex_i16(iop.thing_id);
    print(" type=0x");
    print_hex_i16(iop.type_id);
end sub;

sub do_call(kind: [int8], iop: [CallIop])
    print(kind);
    print(" id=0x");
    print_hex_i16(iop.thing_id);
    print(" parameter_count=");
    print_i8(iop.parameter_count);
end sub;

sub do_goto(iop: [GotoIop])
    print("GOTO label=0x");
    print_label(iop.label_id);
end sub;

sub do_typed(kind: [int8], iop: [TypedIop])
    print(kind);
    print(" type_id=0x");
    print_hex_i16(iop.type_id);
end sub;

sub do_conditional(kind: [int8], iop: [ConditionalIop])
    do_typed(kind, iop as [TypedIop]);
    print(" iftrue=0x");
    print_hex_i16(iop.iftrue_label_id);
    print(" iffalse=0x");
    print_hex_i16(iop.iffalse_label_id);
end sub;

loop
    var header: [IHeader] := read_iop_header();
    if header.thing_id == 0 then
        break;
    end if;

    var subroutine: [SubroutineThing] := find_thing(header.thing_id) as [SubroutineThing];
    var symbol_id: uint16 := find_symbol_by_value(header.thing_id);
    print("OBJECT: id=0x");
    print_hex_i16(header.thing_id);
    print(", section=");
    print_i8(header.section);
    print(": ");
    if symbol_id != 0 then
        var symbol: [SymbolThing] := find_thing(symbol_id) as [SymbolThing];
        print_identifier(symbol.name_id);
    else
        print("(main subroutine)");
    end if;
    print_newline();

    var offset: uint16 := IHeader@bytes;
    loop
        var iop: [Iop] := read_iop();
        var kind: uint8 := iop.iop_kind;
        print_hex_i16(offset);
        print(": ");
        if kind == IOP_END then
            print("END");
        elseif kind == IOP_FILENAME then
            do_filename(iop as [FilenameIop]);
        elseif kind == IOP_LINENUMBER then
            do_linenumber(iop as [LinenumberIop]);
        elseif kind == IOP_LABEL then
            do_label(iop as [LabelIop]);
        elseif kind == IOP_ASSIGN then
            print("ASSIGN");
        elseif kind == IOP_RETURN then
            print("RETURN");
        elseif kind == IOP_SCALL then
            do_call("SCALL", iop as [CallIop]);
        elseif kind == IOP_FCALL then
            do_call("FCALL", iop as [CallIop]);
        elseif kind == IOP_GOTO then
            do_goto(iop as [GotoIop]);
        elseif kind == IOP_BEQ then
            do_conditional("BEQ", iop as [ConditionalIop]);
        elseif kind == IOP_BLT then
            do_conditional("BLT", iop as [ConditionalIop]);
        elseif kind == IOP_BGT then
            do_conditional("BGT", iop as [ConditionalIop]);
        elseif kind == IOP_THING then
            do_thing(iop as [ThingIop]);
        elseif kind == IOP_ADDOP then
            do_typed("ADDOP", iop as [TypedIop]);
        elseif kind == IOP_SUBOP then
            do_typed("SUBOP", iop as [TypedIop]);
        elseif kind == IOP_MULOP then
            do_typed("MULOP", iop as [TypedIop]);
        elseif kind == IOP_DIVOP then
            do_typed("DIVOP", iop as [TypedIop]);
        elseif kind == IOP_MODOP then
            do_typed("MODOP", iop as [TypedIop]);
        elseif kind == IOP_ANDOP then
            do_typed("ANDOP", iop as [TypedIop]);
        elseif kind == IOP_OROP then
            do_typed("OROP", iop as [TypedIop]);
        elseif kind == IOP_EOROP then
            do_typed("EOROP", iop as [TypedIop]);
        elseif kind == IOP_NEGOP then
            do_typed("NEGOP", iop as [TypedIop]);
        elseif kind == IOP_NOTOP then
            do_typed("NOTOP", iop as [TypedIop]);
        elseif kind == IOP_INDEXOP then
            do_typed("INDEXOP", iop as [TypedIop]);
        elseif kind == IOP_MEMBEROP then
            do_typed("MEMBEROP", iop as [TypedIop]);
        elseif kind == IOP_ADDRESSOP then
            do_typed("ADDRESSOP", iop as [TypedIop]);
        elseif kind == IOP_CASTOP then
            do_typed("CASTOP", iop as [TypedIop]);
        else
            print("Unknown iop 0x");
            print_hex_i8(kind);
        end if;
        print_newline();
        offset := offset + iop.iop_size;
        if kind == IOP_END then
            break;
        end if;
    end loop;
    print_newline();
end loop;
