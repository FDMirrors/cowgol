sub yyparse(): (result: int8)
    var stack: StackEntry[STACK_SIZE];
    var ps: [StackEntry];
    var r: int16;
    var h: int16;
    var s: int16;
    var n: int16;
    var tk: int16;

    ps := &stack[0];
    s := INITIAL_STATE;
    ps.state := s;
    tk := -1;

do_loop:
    n := state_to_displacement[s as state_to_displacement@index];
    if (tk < 0) and (n > -NUMBER_OF_TOKENS) then
        tk := yylex();
    end if;
    n := n + tk;
    if (n < 0) or (n >= actions_and_gotos@size) or (checking_table[n as checking_table@index] != tk) then
        r := state_to_reduce[s as state_to_reduce@index];
        if r < 0 then
            result := -1;
            return;
        end if;
        goto do_reduce;
    end if;

    n := actions_and_gotos[n as actions_and_gotos@index];
    if n == -1 then
        result := -1;
        return;
    end if;
    if n < 0 then
        r := 0 - (n+2);
        goto do_reduce;
    end if;

    tk := -1;
    copy_yystype(&yylval, &yyval);
do_stack:
    ps := ps + 1;
    if ps >= &stack[STACK_SIZE] then
        result := -2;
        return;
    end if;
    s := n;
    ps.state := s;
    copy_yystype(&yyval, &ps.val);
    goto do_loop;

do_reduce:
    ps := ps - rule_to_arity_table[r as rule_to_arity_table@index];
    h := rule_to_symbol[r as rule_to_symbol@index];
    s := ps.state;
    n := nt_to_displacement[h as nt_to_displacement@index] + s;
    if (n < 0) or (n >= actions_and_gotos@size) or (checking_table[n as checking_table@index] != (NUMBER_OF_TOKENS+h)) then
        n := nt_to_goto[h as nt_to_goto@index];
    else
        n := actions_and_gotos[n as actions_and_gotos@index];
    end if;

    parser_action(r);
    if r == 0 then
        result := 0;
        return;
    end if;
    goto do_stack;
end sub;
