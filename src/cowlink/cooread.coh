var current_coo_id: uint8 := 1;
var pushed_char: uint8 := 0;

sub getchar(fcb: [FCB]): (c: uint8)
	if pushed_char == 0 then
		c := FCBGetChar(fcb);
	else
		c := pushed_char;
		pushed_char := 0;
	end if;
end sub;

sub pushchar(c: uint8)
	pushed_char := c;
end sub;

sub read_hex(fcb: [FCB], len: uint8): (val: uint32)
	val := 0;
	loop
		var c := getchar(fcb);
		if (c >= '0') and (c <= '9') then
			c := c - '0';
		else
			c := c & ~0x20;
			if (c >= 'A') and (c <= 'F') then
				c := c - ('A' - 10);
			else
				StartError();
				print("invalid hex number in coofile at offset 0x");
				print_hex_i32(FCBPos(fcb) - 1);
				EndError();
			end if;
		end if;
		val := (val << 4) + (c as uint32);

		len := len - 1;
		if len == 0 then
			break;
		end if;
	end loop;
end sub;

sub read_hex2(fcb: [FCB]): (val: uint8)
	val := read_hex(fcb, 2) as uint8;
end sub;

sub read_hex4(fcb: [FCB]): (val: uint16)
	val := read_hex(fcb, 4) as uint16;
end sub;

sub read_string(fcb: [FCB], len: uint8): (ptr: [uint8])
	ptr := Alloc((len+1) as intptr) as [uint8];
	var p := ptr;
	loop
		if len == 0 then
			break;
		end if;
		[p] := getchar(fcb);
		p := p + 1;
		len := len - 1;
	end loop;
end sub;

sub AddRef(subr: [Subroutine], calls: [Subroutine])
	var refid := subr.numRefs;
	subr.numRefs := refid + 1;

	var ref := &subr.refs;
	while refid >= COO_REFS_SIZE loop
		if ref.next == (0 as [References]) then
			ref.next := Alloc(@bytesof References) as [References];
		end if;
		ref := ref.next;
		refid := refid - COO_REFS_SIZE;
	end loop;

	ref.calls[refid as uint8] := calls;
end sub;

sub FindSub(coo: [Coo], id: uint16): (ptr: [[Subroutine]])
	var subindex := &coo.index;
	while id >= COO_INDEX_SIZE loop
		if subindex.next == (0 as [CooIndex]) then
			subindex.next := Alloc(@bytesof CooIndex) as [CooIndex];
		end if;
		subindex := subindex.next;
		id := id - COO_INDEX_SIZE;
	end loop;

	ptr := &subindex.subroutines[id as uint8];
end sub;

sub FindOrCreateSub(coo: [Coo], id: uint16): (subroutine: [Subroutine])
	var ptr := FindSub(coo, id);
	subroutine := [ptr];
	if subroutine == (0 as [Subroutine]) then
		subroutine := Alloc(@bytesof Subroutine) as [Subroutine];
		subroutine.coo := coo;
		subroutine.next := subroutines;
		subroutines := subroutine;
		subroutine.id := current_id;
		[ptr] := subroutine;
		current_id := current_id + 1;
	end if;
end sub;

sub Deref(subin: [Subroutine]): (subout: [Subroutine])
	subout := subin;
	if subin != (0 as [Subroutine]) then
		var external := subin.external;
		if external != (0 as [External]) then
			subout := external.impl;
		end if;
	end if;
end sub;

sub FindOrCreateExternal(name: string): (external: [External])
	external := externals;
	while external != (0 as [External]) loop
		if StrCmp(external.name, name) == 0 then
			return;
		end if;
		external := external.next;
	end loop;

	external := Alloc(@bytesof External) as [External];
	external.next := externals;
	external.name := StrDup(name);
	externals := external;
end sub;

sub OpenAndLoadCoo(filename: [uint8]): (coo: [Coo])
	coo := Alloc(@bytesof Coo) as [Coo];
	coo.id := current_id;
	current_id := current_id + 1;
	if FCBOpenIn(&coo.fcb, filename) != 0 then
		CannotOpen(filename);
	end if;

	var id: uint16;
	loop
		var c := getchar(&coo.fcb);
		var len := read_hex4(&coo.fcb);
		var here := FCBPos(&coo.fcb);
		var subroutine: [Subroutine];
		if c == 'E' then
			break;
		elseif c == 'S' then
			id := read_hex4(&coo.fcb);
			subroutine := FindOrCreateSub(coo, id);
			subroutine.state := subroutine.state | SUB_IMPLEMENTATION;
		elseif c == 'R' then
			var subrid := read_hex4(&coo.fcb);
			var requiresid := read_hex4(&coo.fcb);
			AddRef(FindOrCreateSub(coo, subrid), FindOrCreateSub(coo, requiresid));
		elseif c == 'W' then
			id := read_hex4(&coo.fcb);
			subroutine := FindOrCreateSub(coo, id);
			var wid := read_hex2(&coo.fcb);
			subroutine.workspaceSize[wid] := read_hex4(&coo.fcb);
		elseif c == 'N' then
			id := read_hex4(&coo.fcb);
			subroutine := FindOrCreateSub(coo, id);
			subroutine.name := read_string(&coo.fcb, (len as uint8) - 4);
		elseif c == 'X' then
			id := read_hex4(&coo.fcb);
			subroutine := FindOrCreateSub(coo, id);
			var name := read_string(&coo.fcb, (len as uint8) - 4);
			var external := FindOrCreateExternal(name);
			if subroutine.external != (0 as [External]) then
				SimpleError("multiple external declarations");
			end if;
			subroutine.external := external;
		else
			print("error: unsupported record '");
			print_char(c);
			print("' in coofile\n");
			ExitWithError();
		end if;
		FCBSeek(&coo.fcb, here + (len as uint32));
	end loop;
end sub;

