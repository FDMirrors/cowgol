sub print(ptr: [uint8])
    loop
        var c := [ptr];
        if c == 0 then
            return;
        end if;
        print_char(c);
        ptr := ptr + 1;
    end loop;
end sub;

sub print_nl()
	print_char('\n');
end sub;

sub print_i32(value: uint32)
    var buffer: uint8[11];
    var pos: uint8 := 0;
    loop
        var rem: uint32;
        (value, rem) := divmodu32(value, 10);
        buffer[pos] := rem as uint8;
        if value == 0 then
            break;
        end if;
        pos := pos + 1;
    end loop;

    loop
        print_char(buffer[pos] + '0');
        if pos == 0 then
            break;
        end if;
        pos := pos - 1;
    end loop;
end sub;

sub print_i16(value: uint16)
    print_i32(value as uint32);
end sub;

sub print_i8(value: uint8)
    print_i32(value as uint32);
end sub;

sub print_hex_i8(value: uint8)
    var i: uint8 := 2;
    loop
        var digit: uint8 := value >> 4;
        if digit < 10 then
            digit := digit + '0';
        else
            digit := digit + ('a' - 10);
        end if;
        print_char(digit);
        value := value << 4;
        i := i - 1;
        if i == 0 then
            break;
        end if;
    end loop;
end sub;

sub print_hex_i16(value: uint16)
    print_hex_i8((value >> 8) as uint8);
    print_hex_i8(value as uint8);
end sub;

sub print_hex_i32(value: uint32)
    print_hex_i8((value >> 24) as uint8);
    print_hex_i8((value >> 16) as uint8);
    print_hex_i8((value >> 8) as uint8);
    print_hex_i8(value as uint8);
end sub;

sub ExitWithError()
	Exit();
end sub;

