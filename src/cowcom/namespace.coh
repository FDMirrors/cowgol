sub LookupSymbolInNamespace(namespace: [Namespace], name: string): (symbol: [Symbol]) is
	symbol := namespace.first;
	while symbol != (0 as [Symbol]) loop
		if StrCmp(symbol.name, name) == 0 then
			while symbol.kind == TYPEDEF loop
				symbol := symbol.alias;
			end loop;
			return;
		end if;
		symbol := symbol.next;
	end loop;
	symbol := (0 as [Symbol]);
end sub;

sub LookupSymbol(namespace: [Namespace], name: string): (symbol: [Symbol]) is
	if namespace == (0 as [Namespace]) then
		namespace := &current_subr.namespace;
	end if;
	
	while namespace != (0 as [Namespace]) loop
		symbol := LookupSymbolInNamespace(namespace, name);
		if symbol != (0 as [Symbol]) then
			return;
		end if;
		namespace := namespace.parent;
	end loop;

	symbol := (0 as [Symbol]);
end sub;

sub AddToNamespace(namespace: [Namespace], symbol: [Symbol]) is
	if namespace.last == (0 as [Symbol]) then
		namespace.first := symbol;
		namespace.last := symbol;
	else
		namespace.last.next := symbol;
		namespace.last := symbol;
	end if;
end sub;

# Takes ownership of the string
sub AddSymbol(namespace: [Namespace], name: string): (symbol: [Symbol]) is
	if namespace == (0 as [Namespace]) then
		namespace := &current_subr.namespace;
	end if;

	symbol := Alloc(@bytesof Symbol) as [Symbol];
	if name != (0 as string) then
		if LookupSymbolInNamespace(namespace, name) != (0 as [Symbol]) then
			StartError();
			print("symbol '");
			print(name);
			print("' is already defined");
			EndError();
		end if;

		# Only add the symbol to the namespace if it has a name.
		symbol.name := name;
		AddToNamespace(namespace, symbol);
	end if;
end sub;

# Takes ownership of the name
sub AddAlias(namespace: [Namespace], name: string, real: [Symbol]): (symbol: [Symbol]) is
	symbol := AddSymbol(namespace, name);
	symbol.kind := TYPEDEF;
	symbol.alias := real;
end sub;

sub AddAliasString(name: string, real: [Symbol]) is
	var symbol := AddAlias(0 as [Namespace], name, real);
end sub;

sub CheckNotPartialType(type: [Symbol]) is
	if type.typedata.kind == TYPE_PARTIAL then
		StartError();
		print("'");
		print(type.name);
		print("' is a partial type");
		EndError();
	end if;
end sub;

sub MakeNumberType(width: uint8, alignment: uint8, is_signed: uint8, name: string): (symbol: [Symbol]) is
	symbol := Alloc(@bytesof Symbol) as [Symbol];
	symbol.kind := TYPE;
	symbol.typedata.kind := TYPE_NUMBER;
	symbol.typedata.width := width as uint16;
	symbol.typedata.stride := width as uint16;
	symbol.typedata.alignment := alignment;
	symbol.typedata.numbertype.is_signed := is_signed;
	symbol.name := name;
	AddToNamespace(&current_subr.namespace, symbol);
end sub;

sub IsTypeOfKind(type: [Symbol], kind: uint8): (result: uint8) is
	result := 0;
	if (type != (0 as [Symbol])) and (type.kind == TYPE) and (type.typedata.kind == kind) then
		result := 1;
	end if;
end sub;

sub IsArray(type: [Symbol]): (result: uint8) is
	result := IsTypeOfKind(type, TYPE_ARRAY);
end sub;

sub IsPtr(type: [Symbol]): (result: uint8) is
	result := IsTypeOfKind(type, TYPE_POINTER);
end sub;

sub IsSubroutine(type: [Symbol]): (result: uint8) is
	result := IsTypeOfKind(type, TYPE_SUBROUTINE);
end sub;

sub IsNum(type: [Symbol]): (result: uint8) is
	if type == (0 as [Symbol]) then
		result := 1;
	else
		result := IsTypeOfKind(type, TYPE_NUMBER);
	end if;
end sub;

sub IsSNum(type: [Symbol]): (result: uint8) is
	if type == (0 as [Symbol]) then
		result := 1;
	elseif (IsTypeOfKind(type, TYPE_NUMBER) != 0) and (type.typedata.numbertype.is_signed != 0) then
		result := 1;
	else
		result := 0;
	end if;
end sub;

sub IsScalar(type: [Symbol]): (result: uint8) is
	if (IsPtr(type) != 0) or (IsNum(type) != 0) then
		result := 1;
	else
		result := 0;
	end if;
end sub;

sub IsRecord(type: [Symbol]): (result: uint8) is
	result := IsTypeOfKind(type, TYPE_RECORD);
end sub;

sub MakeLValue(address: [Node]): (lvalue: [Node]) is
	var w: uint8 := 0;
	var elementtype := address.type.typedata.pointertype.element;
	if IsScalar(elementtype) != 0 then
		w := elementtype.typedata.width as uint8;
	end if;
	lvalue := MidLoad(w, address);
	lvalue.type := elementtype;

	#print("make ");
	#print_hex_i32(lvalue as intptr as uint32);
	#print_nl();
end sub;

sub UndoLValue(lvalue: [Node]): (address: [Node]) is
	var k := lvalue.op;
	if (k < MIDCODE_LOAD0) or (k > MIDCODE_LOAD8) then
		SimpleError("lvalue required");
	end if;

	address := lvalue.left;
	lvalue.left := (0 as [Node]);
	Discard(lvalue);
end sub;

# Copies a parameter symbol to the new subroutine. The parameter is still owned
# by the old subroutine. (This is used for copying parameters from interfaces
# to their implementations.)
sub CopyParameter(param: [Symbol], subr: [Subroutine]): (newparam: [Symbol]) is
end sub;


