var iop_in_fd: int8;
var iop_buffer: uint8[256];
var current_filename_id: uint16;
var current_linenumber: uint16;

sub init_iop_reader(filename: [int8])
    iop_in_fd := file_openin(filename);
    current_filename_id := 0;
    current_linenumber := 0;
end sub;

sub deinit_iop_reader()
    file_close(iop_in_fd);
end sub;

sub halt()
    print(" at ");
    show_identifier(current_filename_id);
    print_char(':');
    print_i16(current_linenumber);
    print_newline();
    exit(1);
end sub;

sub read_iop_header(): (header: [IHeader])
    var eof: uint8 := file_getblock(iop_in_fd, &iop_buffer[0] as [int8], IHeader@bytes);
    if eof != 0 then
        print("unexpected EOF");
        halt();
    end if;
    header := &iop_buffer[0] as [IHeader];
end sub;

sub read_iop(): (iop: [Iop])
    loop
        var eof: uint8;
        var size: int8;
        (size, eof) := file_getchar(iop_in_fd);
        iop_buffer[0] := size as uint8;
        (eof) := file_getblock(iop_in_fd, &iop_buffer[1] as [int8], (size - 1) as uint16);
        iop := &iop_buffer[0] as [Iop];

        if iop.iop_kind == IOP_LINENUMBER then
            var linenumber_iop: [LinenumberIop] := iop as [LinenumberIop];
            current_linenumber := linenumber_iop.line_number;
        elseif iop.iop_kind == IOP_FILENAME then
            var filename_iop: [FilenameIop] := iop as [FilenameIop];
            current_filename_id := filename_iop.thing_id;
        else
            break;
        end if;
    end loop;
end sub;
