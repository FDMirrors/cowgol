sub arch_gen_shift_wide(iop: [BEIop], helper: uint16)
    $if DEBUG
        print("* GENERATE wide shift ");
        print_thing(helper);
        print(": ");
        print_ea(&iop.left);
        print(" + ");
        print_ea(&iop.right);
        print(" -> ");
        print_ea(&iop.dest);
        print_newline();
    $endif

    load_complex_8bit_value_fixed(REG_B, &iop.right);
    
    var indea: EffectiveAddress;
    make_pointer(&iop.left, &indea);
    reg_find_or_load_fixed(REG_HL, &indea);

    make_pointer(&iop.dest, &indea);
    reg_find_or_load_fixed(REG_DE, &indea);

    reg_evict(REG_8BIT);

    write_byte(0xCD); # call
    write_address_iop(IOP_ADDRESS, find_bootstrap_symbol(helper), 0);
end sub;

sub arch_gen_helper_divmod_byte(iop: [BEIop], helper: uint16, resultreg: RegSet)
    $if DEBUG
        print("* GENERATE byte divmod ");
        print_thing(helper);
        print(": ");
        print_ea(&iop.left);
        print(" / ");
        print_ea(&iop.right);
        print(" -> ");
        print_ea(&iop.dest);
        print_newline();
    $endif

    reg_value_is_changing(&iop.dest);
    load_complex_8bit_value_fixed(REG_C, &iop.left);
    load_complex_8bit_value_fixed(REG_D, &iop.right);
    reg_evict(REG_8BIT);

    write_byte(0xCD); # call
    write_address_iop(IOP_ADDRESS, find_bootstrap_symbol(helper), 0);

    save_8bit_or_make_precious(&iop.dest, resultreg);
end sub;

sub arch_gen_helper_divmod_word(iop: [BEIop], helper: uint16, resultreg: RegSet)
    $if DEBUG
        print("* GENERATE word divmod ");
        print_thing(helper);
        print(": ");
        print_ea(&iop.left);
        print(" / ");
        print_ea(&iop.right);
        print(" -> ");
        print_ea(&iop.dest);
        print_newline();
    $endif

    reg_value_is_changing(&iop.dest);
    load_complex_16bit_value_fixed(REG_DE, &iop.left);
    load_complex_16bit_value_fixed(REG_BC, &iop.right);
    reg_evict(REG_8BIT);

    write_byte(0xCD); # call
    write_address_iop(IOP_ADDRESS, find_bootstrap_symbol(helper), 0);

    save_16bit_or_make_precious(&iop.dest, resultreg);
end sub;
