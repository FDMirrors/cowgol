var expr_true_label: uint8;
var expr_false_label: uint8;
var data_stack_size: uint8;

sub expression_state_machine()
    const STATE_BEFORE := 0;
    const STATE_AFTER := 1;

    var operator_stack: uint16[32];
    var operator_stack_pos: uint8 := 0;
    var state: uint8 := STATE_BEFORE;
    var current_parameter_count: uint8 := 0;
    var current_function_call: uint16 := 0;
    var nesting: uint8 := 0;
    data_stack_size := 0;

    sub peek_op(): (op_id: uint16)
        if operator_stack_pos == 0 then
            op_id := 0;
        else
            op_id := operator_stack[operator_stack_pos-1];
        end if;
    end sub;

    sub push_op(op: uint16)
        if operator_stack_pos == operator_stack@size then
            print("expression too complex");
            halt();
        end if;

        operator_stack[operator_stack_pos] := op;
        operator_stack_pos := operator_stack_pos + 1;
    end sub;

    sub discard_op()
        if operator_stack_pos == 0 then
            print("expression stack underflow");
            halt();
        end if;

        operator_stack_pos := operator_stack_pos - 1;
    end sub;

    sub pop_op(): (op: uint16)
        discard_op();
        op := operator_stack[operator_stack_pos];
    end sub;

    sub check_b_op()
        if (expr_true_label == 0) or (expr_false_label == 0) then
            print("boolean operator cannot be used in this context");
            halt();
        end if;
    end sub;

    sub check_alu_op(min_count: uint8)
        if data_stack_size < min_count then
            print("arithmetic operator cannot be used in this context");
            halt();
        end if;
    end sub;

    sub print_op_stack()
        print("op stack:");
        var i: uint8 := operator_stack_pos;
        while i > 0 loop
            print_char(' ');
            i := i - 1;
            var op: uint16 := operator_stack[i];
            var thing: [Thing] := find_thing(op);
            print_hex_i16(op);
            if thing.thing_type == THING_TOKEN then
                show_identifier(op);
            else
                print("<data>");
            end if;
        end loop;
        print_newline();
    end sub;

    sub apply_op()
        #print("applying "); print_op_stack();
        var op_id: uint16 := pop_op();
        var op: [Thing] := find_thing(op_id);

        sub alu_2op(iop: uint8)
            check_alu_op(2);
            emit_fe(iop);
            data_stack_size := data_stack_size - 1;
        end sub;

        sub alu_1op(iop: uint8)
            check_alu_op(1);
            emit_fe(iop);
        end sub;

        sub b_2op(iop: uint8)
            check_b_op();
            emit_fe_conditional_branch(iop, expr_true_label, expr_false_label);
            data_stack_size := data_stack_size - 2;
        end sub;

        sub b_2op_reverse(iop: uint8)
            check_b_op();
            emit_fe_conditional_branch(iop, expr_false_label, expr_true_label);
            data_stack_size := data_stack_size - 2;
        end sub;

        sub b_not()
            check_b_op();
            var t: uint8 := expr_true_label;
            expr_true_label := expr_false_label;
            expr_false_label := t;
        end sub;

        sub parameter()
            check_alu_op(1);
            emit_fe_parameter(IOP_FEX_INPUTPARAM, current_function_call, current_parameter_count);
        end sub;

        sub comma()
            check_alu_op(1);
            current_parameter_count := current_parameter_count + 1;
        end sub;

        sub call()
            var i: uint8 := current_parameter_count;
            while i > 0 loop
                i := i - 1;
                emit_fe_parameter(IOP_FEX_INPUTPARAM, current_function_call, i);
                emit_untyped_fe(IOP_FEX_SWAP);
                emit_untyped_fe(IOP_FEX_ASSIGN);
                data_stack_size := data_stack_size - 1;
            end loop;

            var iop: [CallIop] := emit(IOP_CALL, CallIop@bytes) as [CallIop];
            iop.thing_id := current_function_call;
            iop.input_parameter_count := current_parameter_count;
            iop.output_parameter_count := 1;

            emit_fe_parameter(IOP_FEX_OUTPUTPARAM, current_function_call, 0);
            emit_fe(IOP_FE_MATERIALISE);
            data_stack_size := data_stack_size + 1;

            current_parameter_count := pop_op() as uint8;
            current_function_call := pop_op();
        end sub;

        if op_id == TOKEN_PLUS then
            alu_2op(IOP_FE_ADD);
        elseif op_id == TOKEN_MINUS then
            alu_2op(IOP_FE_SUB);
        elseif op_id == TOKEN_STAR then
            alu_2op(IOP_FE_MUL);
        elseif op_id == TOKEN_SLASH then
            alu_2op(IOP_FE_DIV);
        elseif op_id == TOKEN_PERCENT then
            alu_2op(IOP_FE_MOD);
        elseif op_id == TOKEN_PIPE then
            alu_2op(IOP_FE_OR);
        elseif op_id == TOKEN_AMPERSAND then
            alu_2op(IOP_FE_AND);
        elseif op_id == TOKEN_CARET then
            alu_2op(IOP_FE_EOR);
        elseif op_id == TOKEN_LEFTSHIFT then
            alu_2op(IOP_FE_LSHIFT);
        elseif op_id == TOKEN_RIGHTSHIFT then
            alu_2op(IOP_FE_RSHIFT);
        elseif op_id == TOKEN_CLOSESQUARE then
            alu_2op(IOP_FE_INDEX);
        elseif op_id == TOKEN_CLOSEPAREN then
            # do nothing
        elseif op_id == TOKEN_TILDE then
            alu_1op(IOP_FE_NOT);
        elseif op_id == TOKEN_UNARYAMPERSAND then
            alu_1op(IOP_FE_ADDRESSOF);
        elseif op_id == TOKEN_UNARYMINUS then
            alu_1op(IOP_FE_NEG);
        elseif op_id == TOKEN_EQOP then
            b_2op(IOP_FEX_BEQ);
        elseif op_id == TOKEN_NEOP then
            b_2op_reverse(IOP_FEX_BEQ);
        elseif op_id == TOKEN_LTOP then
            b_2op(IOP_FEX_BLT);
        elseif op_id == TOKEN_LEOP then
            b_2op_reverse(IOP_FEX_BGT);
        elseif op_id == TOKEN_GTOP then
            b_2op(IOP_FEX_BGT);
        elseif op_id == TOKEN_GEOP then
            b_2op_reverse(IOP_FEX_BLT);
        elseif op_id == TOKEN_NOT then
            b_not();
        elseif (op_id == TOKEN_AND) or (op_id == TOKEN_OR) then
            emit_clonelabel(pop_op() as uint8, expr_false_label);
            emit_clonelabel(pop_op() as uint8, expr_true_label);
        elseif op_id == TOKEN_AS then
            emit_fe_cast(pop_op());
        elseif op_id == TOKEN_DOT then
            check_alu_op(1);
            emit_fe_member(pop_op());
        elseif op_id == TOKEN_COMMA then
            comma();
        elseif op_id == TOKEN_CALL then
            call();
        elseif op_id == TOKEN_BYTES then
            alu_1op(IOP_FE_BYTESOF);
        elseif op_id == TOKEN_SIZE then
            alu_1op(IOP_FE_SIZEOF);
        else
            print("couldn't apply operator ");
            show_identifier(op_id);
            halt();
        end if;
    end sub;

    sub apply_higher_precedence_ops(op_id: uint16)
        var op_p: uint8 := get_token_flags(op_id) & TOKEN_FLAG_PRECEDENCE_MASK;
        while operator_stack_pos > 0 loop
            var topop_id: uint16 := peek_op();
            var topop_p: uint8 := get_token_flags(topop_id) & TOKEN_FLAG_PRECEDENCE_MASK;
            if topop_p <= op_p then
                break;
            end if;
            apply_op();
        end loop;
    end sub;

    sub apply_left_associative(op_id: uint16)
        var op_p: uint8 := get_token_flags(op_id) & TOKEN_FLAG_PRECEDENCE_MASK;
        while operator_stack_pos > 0 loop
            var topop_id: uint16 := peek_op();
            var topop_p: uint8 := get_token_flags(topop_id) & TOKEN_FLAG_PRECEDENCE_MASK;
            if topop_p < op_p then
                break;
            end if;
            apply_op();
        end loop;
    end sub;

    sub flush_ops()
        while operator_stack_pos > 0 loop
            apply_op();
        end loop;
    end sub;

    sub close_expression(target_op: uint16)
        #print("looking for: "); show_identifier(target_op); print_newline();
        loop
            var peeked_op: uint16 := peek_op();
            #print("peeked: "); show_identifier(peeked_op); print_newline();
            if (target_op == TOKEN_CLOSEPAREN) and (peeked_op == TOKEN_CALL) then
                # special case; function calls are closed with ).
                break;
            elseif target_op == peeked_op then
                break;
            elseif (peeked_op == 0) or (peeked_op == TOKEN_CLOSEPAREN) or (peeked_op == TOKEN_CLOSESQUARE) then
                print("mismatched [...] or (...)");
                halt();
            else
                apply_op();
            end if;
        end loop;

        apply_op();
    end sub;

    sub thing_reference(type: [TypeThing]): (pushable_id: uint16)
        if current_token == TOKEN_BYTES then
            pushable_id := add_number_by_value(type.width as int32);
        elseif current_token == TOKEN_SIZE then
            var array_type: [ArrayThing] := type as [ArrayThing];
            if array_type.thing_type != THING_ARRAYT then
                show_identifier(current_token);
                print(" only works on arrays");
                halt();
            end if;
            pushable_id := add_number_by_value(array_type.element_count as int32);
        else
            print("expected a type attribute, got ");
            show_identifier(current_token);
            halt();
        end if;
    end sub;

    var thing: [Thing];
    loop
        var token_flags: uint8;
        $if DEBUG
            print("seen: ");
            show_identifier(current_token);
            print_newline();
            print("loop ");
            print_op_stack();
        $endif
        if state == STATE_BEFORE then
            # Before a leaf.
            if current_token == TOKEN_MINUS then
                current_token := TOKEN_UNARYMINUS;
            elseif current_token == TOKEN_AMPERSAND then
                current_token := TOKEN_UNARYAMPERSAND;
            end if;
            token_flags := get_token_flags(current_token);

            if current_token == TOKEN_OPENPAREN then
                push_op(TOKEN_CLOSEPAREN);
                next_token();
                nesting := nesting + 1;
            elseif (token_flags & TOKEN_FLAG_POSTFIX_OP) != 0 then
                print("can't use ");
                show_identifier(current_token);
                print(" as a prefix operator");
                halt();
            elseif (token_flags & TOKEN_FLAG_PREFIX_OP) != 0 then
                push_op(current_token);
                next_token();
            else
                thing := find_thing(current_token);
                if (thing.thing_type == THING_IDENTIFIER) and (peeked_token == TOKEN_OPENPAREN) then
                    push_op(current_function_call);
                    push_op(current_parameter_count as uint16);
                    current_function_call := current_token;
                    push_op(TOKEN_CALL);
                    next_token();
                    expect_token(TOKEN_OPENPAREN);

                    if current_token == TOKEN_CLOSEPAREN then
                        current_parameter_count := 0;
                        apply_op();
                        next_token();
                        state := STATE_AFTER;
                    else
                        current_parameter_count := 1;
                        nesting := nesting + 1;
                    end if;
                elseif (thing.thing_type == THING_IDENTIFIER) or (thing.thing_type == THING_NUMBER)
                        or (thing.thing_type == THING_STRING) then
                    var pushable_id: uint16 := current_token;
                    if thing.thing_type == THING_IDENTIFIER then
                        var looked_up_id: uint16 := deref_symbol(pushable_id);
                        if looked_up_id != 0 then
                            if is_type_thing(looked_up_id) != 0 then
                                next_token();
                                var looked_up: [TypeThing] := find_thing(looked_up_id) as [TypeThing];
                                pushable_id := thing_reference(looked_up);
                            end if;
                        end if;
                    end if;
                    emit_fe_push(pushable_id);
                    next_token();
                    state := STATE_AFTER;
                    data_stack_size := data_stack_size + 1;
                else
                    print("bad prefix token ");
                    show_identifier(current_token);
                    halt();
                end if;
            end if;
        else
            # After a leaf (this is the only valid place to end an expression).
            token_flags := get_token_flags(current_token);
            if (token_flags & TOKEN_FLAG_EXPR_ENDER) != 0 then
                break;
            elseif (current_token == TOKEN_COMMA) and (nesting == 0) then
                break;
            elseif current_token == TOKEN_OPENSQUARE then
                apply_left_associative(TOKEN_OPENSQUARE);
                push_op(TOKEN_CLOSESQUARE);
                state := STATE_BEFORE;
                nesting := nesting + 1;
                next_token();
            elseif (current_token == TOKEN_CLOSEPAREN) or (current_token == TOKEN_CLOSESQUARE) then
                if nesting > 0 then
                    close_expression(current_token);
                    nesting := nesting - 1;
                    next_token();
                else
                    break;
                end if;
            elseif (current_token == TOKEN_AND) or (current_token == TOKEN_OR) then
                check_b_op();
                apply_left_associative(current_token);

                if (current_token == TOKEN_AND) then
                    emit_label(expr_true_label);
                    push_op(create_label() as uint16);
                    push_op(expr_false_label as uint16);
                else
                    emit_label(expr_false_label);
                    push_op(expr_true_label as uint16);
                    push_op(create_label() as uint16);
                end if;
                push_op(current_token);

                expr_true_label := create_label();
                expr_false_label := create_label();

                next_token();
                state := STATE_BEFORE;
            elseif (token_flags & TOKEN_FLAG_BINARY_OP) != 0 then
                apply_left_associative(current_token);
                push_op(current_token);
                next_token();
                state := STATE_BEFORE;
            elseif current_token == TOKEN_AS then
                # 'as' is actually a *unary* operator. The thing on the right hand side is a type,
                # not an expression.
                apply_left_associative(current_token);
                next_token();
                check_alu_op(1);
                push_op(read_type());
                push_op(TOKEN_AS);
            elseif current_token == TOKEN_DOT then
                # '.' is actually a *unary* operator. The thing on the right hand side is an
                # identifier, not an expression.
                apply_left_associative(current_token);
                next_token();
                check_alu_op(1);
                push_op(expect_identifier());
                push_op(TOKEN_DOT);
            elseif (token_flags & TOKEN_FLAG_PREFIX_OP) != 0 then
                print("can't use ");
                show_identifier(current_token);
                print(" as a postfix operator");
                halt();
            elseif (token_flags & TOKEN_FLAG_POSTFIX_OP) != 0 then
                push_op(current_token);
                next_token();
            else
                print("couldn't parse operator ");
                show_identifier(current_token);
                halt();
            end if;
        end if;
    end loop;

    flush_ops();
    if operator_stack_pos != 0 then
        print("unmatched expression");
        halt();
    end if;
end sub;

sub do_expression()
    #print("expression start\n");
    expr_true_label := 0;
    expr_false_label := 0;
    expression_state_machine();
    if data_stack_size != 1 then
        print("expression left ");
        print_i8(data_stack_size);
        print(" items on the stack, but expected 1");
        halt();
    end if;
end sub;

sub do_conditional(): (true_label: uint8, false_label: uint8)
    #print("conditional start\n");
    expr_true_label := create_label();
    expr_false_label := create_label();

    expression_state_machine();

    true_label := expr_true_label;
    false_label := expr_false_label;
    if data_stack_size != 0 then
        print("expression left ");
        print_i8(data_stack_size);
        print(" items on the stack, but expected 0");
        halt();
    end if;
end sub;
