%{
	var uint32_type: [Symbol];
	var int32_type: [Symbol];
	var uint16_type: [Symbol];
	var int16_type: [Symbol];
	var uint8_type: [Symbol];
	var int8_type: [Symbol];
	var intptr_type: [Symbol];

	record Extern
		name: string;
		id: uint16;
		next: [Extern];
	end record;

	var externs: [Extern] := (0 as [Extern]);

	sub ArchAlignUp(value: Size, alignment: uint8): (newvalue: Size)
		newvalue := value;
	end sub;

	sub ArchInitTypes()
		uint32_type := MakeNumberType(4, 1, 0, "uint32");
		int32_type := MakeNumberType(4, 1, 1, "int32");
		uint16_type := MakeNumberType(2, 1, 0, "uint16");
		int16_type := MakeNumberType(2, 1, 1, "int16");
		uint8_type := MakeNumberType(1, 1, 0, "uint8");
		int8_type := MakeNumberType(1, 1, 1, "int8");

		intptr_type := uint32_type;
		AddAliasString("intptr", intptr_type);
	end sub;

	sub ArchGuessIntType(min: Arith, max: Arith): (symbol: [Symbol])
		if (min >= 0) and (max <= 255) then
			symbol := uint8_type;
		elseif (min >= -128) and (max <= 127) then
			symbol := int8_type;
		elseif (min >= 0) and (max <= 65535) then
			symbol := uint16_type;
		elseif (min >= -32768) and (max <= 32767) then
			symbol := int16_type;
		elseif (min >= 0) then
			symbol := uint32_type;
		else
			symbol := int32_type;
		end if;
	end sub;

	sub ArchInitVariable(symbol: [Symbol])
		var subr := symbol.vardata.subr;
		var offset := subr.workspace[0];
		symbol.vardata.offset := offset;
		subr.workspace[0] := offset + symbol.vardata.type.typedata.width;
	end sub;

	sub ArchInitMember(containing: [Symbol], member: [Symbol], position: Size)
		member.vardata.offset := position;
		position := position + member.vardata.type.typedata.width;
		if position > containing.typedata.width then
			containing.typedata.width := position;
		end if;
	end sub;

	sub E_symref(sym: [Symbol], off: Size)
		if sym.vardata.externname != (0 as string) then
			E(sym.vardata.externname);
			E_i16(off as int16);
		else
			EmitByte(COO_ESCAPE_WSREF);
			E_h16(sym.vardata.subr.id);
			E_h8(0);
			E_h16(sym.vardata.offset + off);
		end if;
	end sub;

	sub R_flushall()
		RegCacheFlush(ALL_REGS);
	end sub;

	sub R_flush(reg: RegId)
		RegCacheFlush(FindConflictingRegisters(reg));
	end sub;

	sub E_label(label: LabelRef)
		R_flushall();
		E_labelref(label);
		E(":\n");
	end sub;

	sub getreg(reg: RegId, regtable: [RegId], name: string): (result: RegId)
		result := 0;
		while reg != 0 loop
			if (reg & 1) != 0 then
				result := [regtable];
				return;
			end if;
			reg := reg >> 1;
			regtable := @next regtable;
		end loop;

		if result == 0 then
			StartError();
			print("bad ");
			print(name);
			print_char(' ');
			print_hex_i32(reg);
			EndError();
		end if;
	end sub;

	sub loreg(reg: RegId): (loreg: RegId)
		var regs: RegId[] := {
			REG_AL, REG_BL, REG_CL, REG_DL, 0, 0,
			REG_AL, REG_BL, REG_CL, REG_DL, 0, 0,
			REG_AL, REG_AL,
			REG_BL, REG_BL,
			REG_CL, REG_CL,
			REG_DL, REG_DL
		};

		loreg := getreg(reg, &regs[0], "loreg");
	end sub;

	sub wordreg(reg: RegId): (wordreg: RegId)
		var regs: RegId[] := {
			REG_AX, REG_BX, REG_CX, REG_DX, REG_SI, REG_DI,
			REG_AX, REG_BX, REG_CX, REG_DX, REG_SI, REG_DI,
			REG_AX, REG_AX,
			REG_BX, REG_BX,
			REG_CX, REG_CX,
			REG_DX, REG_DX
		};

		wordreg := getreg(reg, &regs[0], "wordreg");
	end sub;

	sub E_reg(reg: RegId)
		var names: string[] := {
			"eax", "ebx", "ecx", "edx", "esi", "edi",
			"ax",  "bx",  "cx",  "dx",  "si",  "di",
			"al", "ah", "bl", "bh", "cl", "ch", "dl", "dh"
		};

		EmitByte('%');
		var p := &names[0];
		while reg != 0 loop
			if (reg & 1) != 0 then
				E([p]);
				return;
			end if;
			reg := reg >> 1;
			p := @next p;
		end loop;

		StartError();
		print("bad reg ");
		print_hex_i32(reg);
		EndError();
	end sub;

	sub E_push(reg: RegId)
		if (reg & REGCLASS_R8) != 0 then
			reg := wordreg(reg);
		end if;
		if (reg & REGCLASS_R16) != 0 then
			E("\tpushw ");
		else
			E("\tpushl ");
		end if;
		E_reg(reg);
		E_nl();
	end sub;

	sub E_pop(reg: RegId)
		R_flush(reg);
		if (reg & REGCLASS_R8) != 0 then
			reg := wordreg(reg);
		end if;
		if (reg & REGCLASS_R16) != 0 then
			E("\tpopw ");
		else
			E("\tpopl ");
		end if;
		E_reg(reg);
		E_nl();
	end sub;

	sub E_loadconst(reg: RegId, value: Arith)
		var cache := RegCacheFindConstant(value as Word) & reg;
		if cache != 0 then
			# The value is already in the desired register.
			return;
		end if;

		R_flush(reg);
		if (reg & REGCLASS_RH8) != 0 then
			value := value & 0xff;
			E("\tmovb ");
		elseif (reg & REGCLASS_R16) != 0 then
			value := value & 0xffff;
			E("\tmovw ");
		else
			E("\tmovl ");
		end if;
		E("$");
		E_i32(value);
		E_comma();
		E_reg(reg);
		E_nl();
		RegCacheLeavesConstant(reg, value as Word);
	end sub;
			
	sub E_loadaddr(reg: RegId, sym: [Symbol], off: Size)
		var cache := RegCacheFindAddress(sym, off) & reg;
		if cache != 0 then
			# The value is already in the desired register.
			return;
		end if;

		R_flush(reg);
		E("\tlea ");
		E_symref(sym, off);
		E_comma();
		E_reg(reg);
		E_nl();
		RegCacheLeavesAddress(reg, sym, off);
	end sub;
			
	sub E_mov(reg: RegId)
		if (reg & REGCLASS_RH8) != 0 then
			E("\tmovb ");
		elseif (reg & REGCLASS_R16) != 0 then
			E("\tmovw ");
		else
			E("\tmovl ");
		end if;
	end sub;

	sub E_move(src: RegId, dest: RegId)
		R_flush(dest);
		E_mov(src);
		E_reg(src);
		E_comma();
		E_reg(dest);
		E_nl();
	end sub;

	sub E_load(reg: RegId, sym: [Symbol], off: Size, index: RegId)
		var cache := RegCacheFindValue(sym, off) & reg;
		if cache != 0 then
			# The value is already in the desired register.
			return;
		end if;

		R_flush(reg);
		E_mov(reg);
		E("(");
		E_symref(sym, off);
		if index != 0 then
			E_comma();
			E_reg(index);
		end if;
		E("), ");
		E_reg(reg);
		E_nl();
		RegCacheLeavesValue(reg, sym, off);
	end sub;

	sub E_store(reg: RegId, sym: [Symbol], off: Size, index: RegId)
		E_mov(reg);
		E_reg(reg);
		E(", (");
		E_symref(sym, off);
		if index != 0 then
			E_comma();
			E_reg(index);
		end if;
		E(")\n");
		RegCacheLeavesValue(reg, sym, off);
	end sub;

	sub E_loadix(reg: RegId, ptr: RegId, index: RegId, off: Arith)
		R_flush(reg);
		E_mov(reg);
		if off != 0 then
			EmitByte('0');
			E_i32(off);
		end if;
		EmitByte('(');
		E_reg(ptr);
		if index != 0 then
			E_comma();
			E_reg(index);
		end if;
		E("), ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_storeix(reg: RegId, ptr: RegId, index: RegId, off: Arith)
		E_mov(reg);
		E_reg(reg);
		E_comma();
		if off != 0 then
			EmitByte('0');
			E_i32(off);
		end if;
		EmitByte('(');
		E_reg(ptr);
		if index != 0 then
			E_comma();
			E_reg(index);
		end if;
		E(")\n");
	end sub;

	sub E_jump(insn: string, label: LabelRef)
		R_flushall();
		E_tab();
		E(insn);
		E_space();
		E_labelref(label);
		E_nl();
	end sub;

	sub E_jmp(label: LabelRef)
		E_jump("jmp", label);
	end sub;

	sub E_jne(label: LabelRef)
		E_jump("jne", label);
	end sub;

	sub E_call(subr: [Subroutine])
		R_flushall();
		E_tab();
		E("call ");
		E_subref(subr);
		E_nl();
	end sub;

	sub E_ret()
		E("\tret\n");
	end sub;

	sub E_cbw()
		E("\tcbw\n");
	end sub;

	sub E_cwd()
		E("\tcwd\n");
	end sub;

	sub E_cdq()
		E("\tcdq\n");
	end sub;

	sub E_insn(insn: string, reg: RegId)
		E_tab();
		E(insn);
		if (reg & REGCLASS_RH8) != 0 then
			EmitByte('b');
		elseif (reg & REGCLASS_R16) != 0 then
			EmitByte('w');
		else
			EmitByte('l');
		end if;
		E_space();
	end sub;

	sub E_alu1(insn: string, reg: RegId)
		R_flush(reg);
		E_insn(insn, reg);
		E_reg(reg);
		E_nl();
	end sub;

	sub E_neg(reg: RegId)
		E_alu1("neg", reg);
	end sub;

	sub E_not(reg: RegId)
		E_alu1("not", reg);
	end sub;

	sub E_inc(reg: RegId)
		E_alu1("inc", reg);
	end sub;

	sub E_dec(reg: RegId)
		E_alu1("dec", reg);
	end sub;

	sub E_shift(insn: string, reg: RegId)
		R_flush(reg);
		E_insn(insn, reg);
		E("%cl, ");
		E_reg(reg);
		E_nl();
	end sub;

	sub E_shl(reg: RegId)
		E_shift("shl", reg);
	end sub;

	sub E_shr(reg: RegId)
		E_shift("shr", reg);
	end sub;

	sub E_sar(reg: RegId)
		E_shift("sar", reg);
	end sub;

	sub E_shifti(insn: string, value: Arith, reg: RegId)
		R_flush(reg);
		E_insn(insn, reg);
		EmitByte('$');
		E_i32(value);
		E_comma();
		E_reg(reg);
		E_nl();
	end sub;

	sub E_shli(value: Arith, reg: RegId)
		E_shifti("shl", value, reg);
	end sub;

	sub E_sari(value: Arith, reg: RegId)
		E_shifti("sar", value, reg);
	end sub;

	sub E_shri(value: Arith, reg: RegId)
		E_shifti("shr", value, reg);
	end sub;

	sub E_alu2_noflush(insn: string, src: RegId, dest: RegId)
		E_insn(insn, src);
		E_reg(src);
		E_comma();
		E_reg(dest);
		E_nl();
	end sub;

	sub E_cmp(src: RegId, dest: RegId)
		E_alu2_noflush("cmp", src, dest);
	end sub;

	sub E_alu2(insn: string, src: RegId, dest: RegId)
		R_flush(dest);
		E_alu2_noflush(insn, src, dest);
	end sub;

	sub E_xor(src: RegId, dest: RegId)
		E_alu2("xor", src, dest);
		if src == dest then
			RegCacheLeavesConstant(dest, 0);
		end if;
	end sub;

	sub E_add(src: RegId, dest: RegId)
		E_alu2("add", src, dest);
	end sub;

	sub E_sub(src: RegId, dest: RegId)
		E_alu2("sub", src, dest);
	end sub;

	sub E_and(src: RegId, dest: RegId)
		E_alu2("and", src, dest);
	end sub;

	sub E_or(src: RegId, dest: RegId)
		E_alu2("or", src, dest);
	end sub;

	sub E_alu2i_noflush(insn: string, value: Arith, dest: RegId)
		E_insn(insn, dest);
		EmitByte('$');
		E_i32(value);
		E_comma();
		E_reg(dest);
		E_nl();
	end sub;

	sub E_alu2i(insn: string, value: Arith, dest: RegId)
		R_flush(dest);
		E_alu2i_noflush(insn, value, dest);
	end sub;

	sub E_cmpi(value: Arith, dest: RegId)
		E_alu2i_noflush("cmp", value, dest);
	end sub;

	sub E_addi(value: Arith, dest: RegId)
		case value is
			when 1: E_inc(dest);
			when -1: E_dec(dest);
			when else: E_alu2i("add", value, dest);
		end case;
	end sub;

	sub E_subi(value: Arith, dest: RegId)
		case value is
			when 1: E_dec(dest);
			when -1: E_inc(dest);
			when else: E_alu2i("sub", value, dest);
		end case;
	end sub;

	sub E_andi(value: Arith, dest: RegId)
		E_alu2i("and", value, dest);
	end sub;

	sub E_ori(value: Arith, dest: RegId)
		E_alu2i("or", value, dest);
	end sub;

	sub E_xori(value: Arith, dest: RegId)
		E_alu2i("xor", value, dest);
	end sub;

	sub E_imul(dest: RegId)
		if (dest & REGCLASS_R8) != 0 then
			R_flush(REG_AX);
		elseif (dest & REGCLASS_R16) != 0 then
			R_flush(REG_DX|REG_AX);
		else
			R_flush(REG_EDX|REG_EAX);
		end if;
		E("\timul ");
		E_reg(dest);
		E_nl();
	end sub;

	sub E_div(src: RegId)
		if (src & REGCLASS_R8) != 0 then
			R_flush(REG_AX);
		elseif (src & REGCLASS_R16) != 0 then
			R_flush(REG_DX|REG_AX);
		else
			R_flush(REG_EDX|REG_EAX);
		end if;
		R_flush(src);
		E_insn("div", src);
		E_reg(src);
		E_nl();
	end sub;

	sub E_idiv(src: RegId)
		if (src & REGCLASS_R8) != 0 then
			R_flush(REG_AX);
		elseif (src & REGCLASS_R16) != 0 then
			R_flush(REG_DX|REG_AX);
		else
			R_flush(REG_EDX|REG_EAX);
		end if;
		R_flush(src);
		E_insn("idiv", src);
		E_reg(src);
		E_nl();
	end sub;

	sub E_ext(src: RegId, dest: RegId, sext: uint8)
		E_tab();
		E("mov");
		if sext != 0 then
			EmitByte('s');
		else
			EmitByte('z');
		end if;
		E("x ");
		E_reg(src);
		E_comma();
		E_reg(dest);
		E_nl();
	end sub;

	var stringid: uint16 := 0;
	sub E_string(data: string)
		var sid := stringid;
		stringid := stringid + 1;

		EmitterPushChunk();
		E_h16(current_subr.id);

		E("\t.text 1\n");
		EmitByte(COO_ESCAPE_THISCOO);
		EmitByte('c');
		E_u16(sid);
		E(":\n\t.byte ");

		loop
			var c := [data];
			if c == 0 then
				break;
			end if;
			data := data + 1;
			
			E_u8(c);
			E_comma();
		end loop;
		E("0\n");
		EmitterPopChunk('S');

		EmitByte(COO_ESCAPE_THISCOO);
		EmitByte('c');
		E_u16(sid);
	end sub;

	sub ArchEmitMove(src: RegId, dest: RegId)
		if src == 0 then
			E_pop(dest);
		elseif dest == 0 then
			E_push(src);
		else
			E_move(src, dest);
		end if;
	end sub;

	sub ArchEndInstruction()
	end sub;

	sub ArchEndGroup()
	end sub;
%}

wordsize uint32;

register eax ebx ecx edx esi edi;
register  ax  bx  cx  dx  si  di;
register al ah bl bh cl ch dl dh;
register stk4 param;

regclass r8 := al|bl|cl|dl;
regclass rh8 := r8|ah|bh|ch|dh;
regclass r16 := ax|bx|cx|dx|si|di;
regclass r32 := eax|ebx|ecx|edx|esi|edi;
regclass rix := ebx|esi|edi;

regdata eax compatible r32 uses eax|ax|ah|al;
regdata ebx compatible r32 uses ebx|bx|bh|bl;
regdata ecx compatible r32 uses ecx|cx|ch|cl;
regdata edx compatible r32 uses edx|dx|dh|dl;
regdata esi compatible r32 uses esi|si;
regdata edi compatible r32 uses edi|di;
regdata ax compatible r16  uses eax|ax|ah|al;
regdata bx compatible r16  uses ebx|bx|bh|bl;
regdata cx compatible r16  uses ecx|cx|ch|cl;
regdata dx compatible r16  uses edx|dx|dh|dl;
regdata si compatible r16  uses esi|si;
regdata di compatible r16  uses edi|di;
regdata al compatible r8   uses eax|ax|ah|al;
regdata bl compatible r8   uses ebx|bx|bh|bl;
regdata cl compatible r8   uses ecx|cx|ch|cl;
regdata dl compatible r8   uses edx|dx|dh|dl;
regdata stk4 stacked;
regdata param stacked;

// --- Core things ----------------------------------------------------------

gen STARTFILE();
gen ENDFILE();

gen LABEL():b
{
	E_label($b.label);
}

gen JUMP():j
{
	E_jmp($j.label);
}

// --- Subroutines ----------------------------------------------------------

gen STARTSUB():s
{
	RegCacheReset();

	EmitterPushChunk();
	E_h16($s.subr.id);

	E("\n\n\t# ");
	E($s.subr.name);
	E(" workspace at ");
	EmitByte(COO_ESCAPE_WSREF);
	E_h16($s.subr.id);
	E("000000");
	E(" length ");
	EmitByte(COO_ESCAPE_WSSIZE);
	E_h16($s.subr.id);
	E("00");
	E_nl();

	E("\t.text\n");
	EmitByte(COO_ESCAPE_THISSUB);
	E(":\n");

	var count := $s.subr.num_input_parameters;
	var lastparam := count - 1;
	var popped: uint8 := 0;

	sub pop_return_address()
		if popped == 0 then
			E_pop(REG_EDX);
			popped := 1;
		end if;
	end sub;

	while count != 0 loop
		count := count - 1;
		var param := GetInputParameter($s.subr, count);

		var reg: RegId;
		case param.vardata.type.typedata.width is
			when 1: reg := REG_AL;
			when 2: reg := REG_AX;
			when 4: reg := REG_EAX;
		end case;

		if count != lastparam then
			pop_return_address();
			E_pop(reg);
		end if;
		E_store(reg, param, 0, 0);
	end loop;

	if popped != 0 then
		E_push(REG_EDX);
	end if;
}

gen ENDSUB():s
{
	R_flushall();

	E("end_");
	E_subref($s.subr);
	E(":\n");

	var count: uint8 := 0;
	var params := $s.subr.num_output_parameters;
	var pushed: uint8 := 0;

	sub push_return_address()
		if pushed == 0 then
			E_pop(REG_EDX);
			pushed := 1;
		end if;
	end sub;

	while count != params loop
		var param := GetOutputParameter($s.subr, count);

		var reg: RegId;
		case param.vardata.type.typedata.width is
			when 1: reg := REG_AL;
			when 2: reg := REG_AX;
			when 4: reg := REG_EAX;
		end case;
		E_load(reg, param, 0, 0);
		if count != (params-1) then
			push_return_address();
			E_push(reg);
		end if;

		count := count + 1;
	end loop;

	if pushed != 0 then
		E("\tjmp *%edx\n");
	else
		E_ret();
	end if;

	EmitterDeclareWorkspace($s.subr, 0, $s.subr.workspace[0]);
	EmitterPopChunk('S');
}

gen RETURN()
{
	if current_subr.num_output_parameters == 0 then
		E_ret();
	else
		E("\tjmp end_");
		E_subref(current_subr);
		E_nl();
	end if;
}

gen CALL(param):s
		{ E_call($s.subr); }
	
gen al := CALLE1(param):s
		{ E_call($s.subr); }

gen ax := CALLE2(param):s
		{ E_call($s.subr); }

gen eax := CALLE4(param):s
		{ E_call($s.subr); }

gen param := END();

gen param := ARG1(param, al, remaining==0);

gen param := ARG1(param, r8:lhs, remaining!=0)
		{ E_push($lhs); }
	
gen param := ARG1(END(), CALLE1(param):s, remaining==0)
		{ E_call($s.subr); }

gen param := ARG2(param, ax, remaining==0);

gen param := ARG2(param, r16:lhs, remaining!=0)
		{ E_push($lhs); }

gen param := ARG2(END(), CALLE2(param):s, remaining==0)
		{ E_call($s.subr); }

gen param := ARG4(param, eax, remaining==0);

gen param := ARG4(param, r32:lhs, remaining!=0)
		{ E_push($lhs); }

gen param := ARG4(END(), CALLE4(param):s, remaining==0)
		{ E_call($s.subr); }

gen al := POPARG1(remaining==0);

gen r8 := POPARG1(remaining!=0)
		{ E_pop($$); }

gen ax := POPARG2(remaining==0);

gen r16 := POPARG2()
		{ E_pop($$); }

gen eax := POPARG4(remaining==0);

gen r32 := POPARG4()
		{ E_pop($$); }

// --- Constants ------------------------------------------------------------

gen r8|r16|r32 := CONSTANT():c
{
	if $c.value == 0 then
		E_xor($$, $$);
	else
		E_loadconst($$, $c.value);
	end if;
}

gen r32 := ADDRESS():a
{
	E_loadaddr($$, $a.sym, $a.off);
}

// --- Loads ---------------------------------------------------------------

gen r8 := LOAD1(r32:rhs)     { E_loadix($$, $rhs, 0, 0); }
gen r16 := LOAD2(r32:rhs)    { E_loadix($$, $rhs, 0, 0); }
gen r32 := LOAD4(r32:rhs)    { E_loadix($$, $rhs, 0, 0); }

gen r8 := LOAD1(ADD4(r32:rhs, r32:idx))  { E_loadix($$, $rhs, $idx, 0); }
gen r16 := LOAD2(ADD4(r32:rhs, r32:idx)) { E_loadix($$, $rhs, $idx, 0); }
gen r32 := LOAD4(ADD4(r32:rhs, r32:idx)) { E_loadix($$, $rhs, $idx, 0); }

gen r8 := LOAD1(ADD4(r32:rhs, CONSTANT():c))  { E_loadix($$, $rhs, 0, $c.value); }
gen r16 := LOAD2(ADD4(r32:rhs, CONSTANT():c)) { E_loadix($$, $rhs, 0, $c.value); }
gen r32 := LOAD4(ADD4(r32:rhs, CONSTANT():c)) { E_loadix($$, $rhs, 0, $c.value); }

gen r8 := LOAD1(ADDRESS():a)  { E_load($$, $a.sym, $a.off, 0); }
gen r16 := LOAD2(ADDRESS():a) { E_load($$, $a.sym, $a.off, 0); }
gen r32 := LOAD4(ADDRESS():a) { E_load($$, $a.sym, $a.off, 0); }

// --- Stores ---------------------------------------------------------------

gen STORE1(r8:lhs, r32:rhs)  { E_storeix($lhs, $rhs, 0, 0); }
gen STORE2(r16:lhs, r32:rhs) { E_storeix($lhs, $rhs, 0, 0); }
gen STORE4(r32:lhs, r32:rhs) { E_storeix($lhs, $rhs, 0, 0); }

gen STORE1(r8:lhs, ADD4(r32:rhs, r32:idx))  { E_storeix($lhs, $rhs, $idx, 0); }
gen STORE2(r16:lhs, ADD4(r32:rhs, r32:idx)) { E_storeix($lhs, $rhs, $idx, 0); }
gen STORE4(r32:lhs, ADD4(r32:rhs, r32:idx)) { E_storeix($lhs, $rhs, $idx, 0); }

gen STORE1(r8:lhs, ADD4(r32:rhs, CONSTANT():c))  { E_storeix($lhs, $rhs, 0, $c.value); }
gen STORE2(r16:lhs, ADD4(r32:rhs, CONSTANT():c)) { E_storeix($lhs, $rhs, 0, $c.value); }
gen STORE4(r32:lhs, ADD4(r32:rhs, CONSTANT():c)) { E_storeix($lhs, $rhs, 0, $c.value); }

gen STORE1(r8:lhs, ADDRESS():a)  { E_store($lhs, $a.sym, $a.off, 0); }
gen STORE2(r16:lhs, ADDRESS():a) { E_store($lhs, $a.sym, $a.off, 0); }
gen STORE4(r32:lhs, ADDRESS():a) { E_store($lhs, $a.sym, $a.off, 0); }

// --- Maths ----------------------------------------------------------------

gen r8  := NEG1($$)                   { E_neg($$); }
gen r16 := NEG2($$)                   { E_neg($$); }
gen r32 := NEG4($$)                   { E_neg($$); }

gen r8  := NOT1($$)                   { E_not($$); }
gen r16 := NOT2($$)                   { E_not($$); }
gen r32 := NOT4($$)                   { E_not($$); }

gen r8  := ADD1($$, r8:rhs)           { E_add($rhs, $$); }
gen r16 := ADD2($$, r16:rhs)          { E_add($rhs, $$); }
gen r32 := ADD4($$, r32:rhs)          { E_add($rhs, $$); }

gen r8  := ADD1($$, CONSTANT():c)     { E_addi($c.value, $$); }
gen r16 := ADD2($$, CONSTANT():c)     { E_addi($c.value, $$); }
gen r32 := ADD4($$, CONSTANT():c)     { E_addi($c.value, $$); }

gen r8  := SUB1($$, r8:rhs)           { E_sub($rhs, $$); }
gen r16 := SUB2($$, r16:rhs)          { E_sub($rhs, $$); }
gen r32 := SUB4($$, r32:rhs)          { E_sub($rhs, $$); }

gen r8  := SUB1($$, CONSTANT():c)     { E_subi($c.value, $$); }
gen r16 := SUB2($$, CONSTANT():c)     { E_subi($c.value, $$); }
gen r32 := SUB4($$, CONSTANT():c)     { E_subi($c.value, $$); }

gen r8  := AND1($$, r8:rhs)           { E_and($rhs, $$); }
gen r16 := AND2($$, r16:rhs)          { E_and($rhs, $$); }
gen r32 := AND4($$, r32:rhs)          { E_and($rhs, $$); }

gen r8  := AND1($$, CONSTANT():c)     { E_andi($c.value, $$); }
gen r16 := AND2($$, CONSTANT():c)     { E_andi($c.value, $$); }
gen r32 := AND4($$, CONSTANT():c)     { E_andi($c.value, $$); }

gen r8  := OR1($$, r8:rhs)            { E_or($rhs, $$); }
gen r16 := OR2($$, r16:rhs)           { E_or($rhs, $$); }
gen r32 := OR4($$, r32:rhs)           { E_or($rhs, $$); }

gen r8  := OR1($$, CONSTANT():c)      { E_ori($c.value, $$); }
gen r16 := OR2($$, CONSTANT():c)      { E_ori($c.value, $$); }
gen r32 := OR4($$, CONSTANT():c)      { E_ori($c.value, $$); }

gen r8  := EOR1($$, r8:rhs)           { E_xor($rhs, $$); }
gen r16 := EOR2($$, r16:rhs)          { E_xor($rhs, $$); }
gen r32 := EOR4($$, r32:rhs)          { E_xor($rhs, $$); }

gen r8  := EOR1($$, CONSTANT():c)     { E_xori($c.value, $$); }
gen r16 := EOR2($$, CONSTANT():c)     { E_xori($c.value, $$); }
gen r32 := EOR4($$, CONSTANT():c)     { E_xori($c.value, $$); }

gen al  := MUL1($$, r8:rhs) uses ah	  { E_imul($rhs); }
gen ax  := MUL2($$, r16:rhs) uses dx  { E_imul($rhs); }
gen eax := MUL4($$, r32:rhs) uses edx { E_imul($rhs); }

gen al := DIVU1(al, bl|cl|dl:rhs) uses ah { E_xor(REG_AH, REG_AH); E_div($rhs); }
gen al := REMU1(al, bl|cl|dl:rhs) uses ah { E_xor(REG_AH, REG_AH); E_div($rhs); E_move(REG_AH, REG_AL); }
gen al := DIVS1(al, bl|cl|dl:rhs) uses ah { E_cbw(); E_idiv($rhs); }
gen al := REMS1(al, bl|cl|dl:rhs) uses ah { E_cbw(); E_idiv($rhs); E_move(REG_AH, REG_AL); }

gen ax := DIVU2(ax, bx|cx|dx:rhs) uses dx { E_xor(REG_DX, REG_DX); E_div($rhs); }
gen dx := REMU2(ax, bx|cx|dx:rhs)         { E_xor(REG_DX, REG_DX); E_div($rhs); }
gen ax := DIVS2(ax, bx|cx|dx:rhs) uses dx { E_cwd(); E_idiv($rhs); }
gen dx := REMS2(ax, bx|cx|dx:rhs)         { E_cwd(); E_idiv($rhs); }

gen eax := DIVU4(eax, ebx|ecx|edx:rhs) uses edx { E_xor(REG_EDX, REG_EDX); E_div($rhs); }
gen edx := REMU4(eax, ebx|ecx|edx:rhs)          { E_xor(REG_EDX, REG_EDX); E_div($rhs); }
gen eax := DIVS4(eax, ebx|ecx|edx:rhs) uses edx { E_cdq(); E_idiv($rhs); }
gen edx := REMS4(eax, ebx|ecx|edx:rhs)          { E_cdq(); E_idiv($rhs); }

gen al|bl|dl            := LSHIFT1($$:lhs, cl:rhs) { E_shl($lhs); }
gen ax|bx|dx|si|di      := LSHIFT2($$:lhs, cl:rhs) { E_shl($lhs); }
gen eax|ebx|edx|esi|edi := LSHIFT4($$:lhs, cl:rhs) { E_shl($lhs); }
gen r8                  := LSHIFT1($$:lhs, CONSTANT():c) { E_shli($c.value, $lhs); }
gen r16                 := LSHIFT2($$:lhs, CONSTANT():c) { E_shli($c.value, $lhs); }
gen r32                 := LSHIFT4($$:lhs, CONSTANT():c) { E_shli($c.value, $lhs); }

gen al|bl|dl            := RSHIFTU1($$:lhs, cl:rhs) { E_shr($lhs); }
gen ax|bx|dx|si|di      := RSHIFTU2($$:lhs, cl:rhs) { E_shr($lhs); }
gen eax|ebx|edx|esi|edi := RSHIFTU4($$:lhs, cl:rhs) { E_shr($lhs); }
gen r8                  := RSHIFTU1($$:lhs, CONSTANT():c) { E_shri($c.value, $lhs); }
gen r16                 := RSHIFTU2($$:lhs, CONSTANT():c) { E_shri($c.value, $lhs); }
gen r32                 := RSHIFTU4($$:lhs, CONSTANT():c) { E_shri($c.value, $lhs); }

gen al|bl|dl            := RSHIFTS1($$:lhs, cl:rhs) { E_sar($lhs); }
gen ax|bx|dx|si|di      := RSHIFTS2($$:lhs, cl:rhs) { E_sar($lhs); }
gen eax|ebx|edx|esi|edi := RSHIFTS4($$:lhs, cl:rhs) { E_sar($lhs); }
gen r8                  := RSHIFTS1($$:lhs, CONSTANT():c) { E_sari($c.value, $lhs); }
gen r16                 := RSHIFTS2($$:lhs, CONSTANT():c) { E_sari($c.value, $lhs); }
gen r32                 := RSHIFTS4($$:lhs, CONSTANT():c) { E_sari($c.value, $lhs); }

// --- Conditionals ---------------------------------------------------------

%{
	sub CmpJumps(trueinsn: string, falseinsn: string, node: [Node])
		if node.beq0.truelabel != node.beq0.fallthrough then
			E_jump(trueinsn, node.beq0.truelabel);
		end if;
		if node.beq0.falselabel != node.beq0.fallthrough then
			E_jump(falseinsn, node.beq0.falselabel);
		end if;
	end sub;

	sub CmpJumpsJe(node: [Node])
		CmpJumps("je", "jne", node);
	end sub;

	sub CmpJumpsJb(node: [Node])
		CmpJumps("jb", "jae", node);
	end sub;

	sub CmpJumpsJl(node: [Node])
		CmpJumps("jl", "jge", node);
	end sub;

	sub beqc(node: [Node], lhs: Arith, rhs: Arith)
		var label := node.beq0.falselabel;
		if lhs == rhs then
			label := node.beq0.truelabel;
		end if;
		if label != node.beq0.fallthrough then
			E_jmp(label);
		end if;
	end sub;
%}

gen BEQ0(CONSTANT():c1, CONSTANT():c2):b
	    { beqc(self.n[0], $c1.value, $c2.value); }

gen BEQ1(r8:lhs, r8:rhs):b        { E_cmp($rhs, $lhs);      CmpJumpsJe(self.n[0]); }
gen BEQ2(r16:lhs, r16:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJe(self.n[0]); }
gen BEQ4(r32:lhs, r32:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJe(self.n[0]); }
gen BEQ1(r8:lhs, CONSTANT():c):b  { E_cmpi($c.value, $lhs); CmpJumpsJe(self.n[0]); }
gen BEQ2(r16:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJe(self.n[0]); }
gen BEQ4(r32:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJe(self.n[0]); }

gen BLTS1(r8:lhs, r8:rhs):b        { E_cmp($rhs, $lhs);      CmpJumpsJl(self.n[0]); }
gen BLTS2(r16:lhs, r16:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJl(self.n[0]); }
gen BLTS4(r32:lhs, r32:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJl(self.n[0]); }
gen BLTS1(r8:lhs, CONSTANT():c):b  { E_cmpi($c.value, $lhs); CmpJumpsJl(self.n[0]); }
gen BLTS2(r16:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJl(self.n[0]); }
gen BLTS4(r32:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJl(self.n[0]); }

gen BLTU1(r8:lhs, r8:rhs):b        { E_cmp($rhs, $lhs);      CmpJumpsJb(self.n[0]); }
gen BLTU2(r16:lhs, r16:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJb(self.n[0]); }
gen BLTU4(r32:lhs, r32:rhs):b      { E_cmp($rhs, $lhs);      CmpJumpsJb(self.n[0]); }
gen BLTU1(r8:lhs, CONSTANT():c):b  { E_cmpi($c.value, $lhs); CmpJumpsJb(self.n[0]); }
gen BLTU2(r16:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJb(self.n[0]); }
gen BLTU4(r32:lhs, CONSTANT():c):b { E_cmpi($c.value, $lhs); CmpJumpsJb(self.n[0]); }

// --- Case -----------------------------------------------------------------

gen STARTCASE1(al);

gen STARTCASE2(ax);

gen STARTCASE4(eax);

gen WHENCASE1():c
{
	E_cmpi($c.value & 0xff, REG_AL);
	E_jne($c.falselabel);
}

gen WHENCASE2():c
{
	E_cmpi($c.value & 0xffff, REG_AX);
	E_jne($c.falselabel);
}

gen WHENCASE4():c
{
	E_cmpi($c.value, REG_EAX);
	E_jne($c.falselabel);
}

gen ENDCASE1();
gen ENDCASE2();
gen ENDCASE4();

// --- Casts ----------------------------------------------------------------

gen r8 :=  CAST21(r16:val) { E_move(loreg($val), $$); }
gen r8 :=  CAST41(r32:val) { E_move(loreg($val), $$); }
gen r16 := CAST42(r32:val) { E_move(wordreg($val), $$); }

gen r16 := CAST12(r8:val):c  { E_ext($val, $$, $c.sext); }
gen r32 := CAST14(r8:val):c  { E_ext($val, $$, $c.sext); }
gen r32 := CAST24(r16:val):c { E_ext($val, $$, $c.sext); }

// --- Strings --------------------------------------------------------------

gen r32 := STRING():s
{
	R_flush($$);
	E("\tlea ");
	E_string($s.text);
	E_comma();
	E_reg($$);
	E_nl();
}

// --- Initialisers ---------------------------------------------------------

gen STARTINIT():s
{
	EmitterPushChunk();
	E_h16(current_subr.id);
	E("\t.data 4\n"); # yes, really
	E($s.sym.vardata.externname);
	E(":\n");
}

gen ENDINIT()
{
	EmitterPopChunk('S');
}

gen INIT1():c
{
	E("\t.byte ");
	E_u8($c.value as uint8);
	E_nl();
}

gen INIT2():c
{
	E("\t.short ");
	E_u16($c.value as uint16);
	E_nl();
}

gen INIT4():c
{
	E("\t.long ");
	E_u32($c.value as uint32);
	E_nl();
}

gen INITS():s
{
	E("\t.long ");
	E_string($s.text);
	E_nl();
}

// --- Inline assembly ------------------------------------------------------

gen ASMSTART()
{
	R_flushall();
	E_tab();
}

gen ASMTEXT():t
{
	E($t.text);
	E_space();
}

gen ASMSYMBOL():s
{
	if $s.sym.kind == VAR then
		E_symref($s.sym, 0);
	else
		E_subref($s.sym.subr);
	end if;
	E_space();
}

gen ASMVALUE():c
{
	EmitByte('(');
	E_i32($c.value);
	EmitByte(')');
}

gen ASMEND()
{
    E_nl();
}



